
% --------------------------------------------------------------------------------
\subsection{Overview}
\cutdef*{subsubsection}
\label{section:ref:functions:overview}

Mythryl is a (mostly-)functional programming language;
functions are accordingly of central importance.

From a strictly formal point of view, every Mythryl 
function has exactly one argument and returns exactly 
one result, which is to say it has type

\begin{verbatim}
    Input_Type -> Output_Type;
\end{verbatim}

Thus, the canonical Mythryl function is something like

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun reverse_string string
        =
        implode (reverse (explode string));

    printf "reverse_string \"abc\" = \"%s\".\n" (reverse_string "abc");

    linux$ ./my-script
    reverse_string "abc" = "cba".
\end{verbatim}

We frequently think of Mythryl functions as taking multiple 
arguments because the input type is often a tuple or record:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun add_three_ints (i, j, k)
        =
        i + j + k;

    printf "Result = %d\n" (add_three_ints (1, 2, 3));

    linux$ ./my-script
    Result = 6
\end{verbatim}

From a formal point of view, however, this is still a function 
taking a single argument, which is then pattern-matched into 
its constituent elements.  This is more than a theoretical 
fiction.  For example, we can compute the argument tuple 
separately and then provide it to the function as a single argument: 

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun add_three_ints (i, j, k)
        =
        i + j + k;

    arg = (1, 2, 3);

    printf "Result = %d\n" (add_three_ints arg);

    linux$ ./my-script
    Result = 6
\end{verbatim}

\cutend*



% --------------------------------------------------------------------------------
\subsection{Implicit Case Expressions in Functions}
\cutdef*{subsubsection}
\label{section:ref:functions:implicit-case-expressions-in-functions}

Mythryl function syntax supports implicit case expressions, allowing 
a function to be expressed as a sequence of {\it pattern} => {\it expression} 
pairs without need to write an explicit {\tt case}.

Thus, the script

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun from_roman string
        =
        case string
             "I"    => 1;
             "II"   => 2;
             "III"  => 3;
             "IV"   => 4;
             "V"    => 5;
             "VI"   => 6;
             "VII"  => 7;
             "VIII" => 8;
             "IX"   => 9;
             _      => raise exception FAIL "Unsupported Roman number";
        esac;

    printf "from_roman III = %d\n" (from_roman "III");

    linux$ ./my-script
    from_roman III = 3
\end{verbatim}

may be written more compactly as

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun from_roman "I"    => 1;
        from_roman "II"   => 2;
        from_roman "III"  => 3;
        from_roman "IV"   => 4;
        from_roman "V"    => 5;
        from_roman "VI"   => 6;
        from_roman "VII"  => 7;
        from_roman "VIII" => 8;
        from_roman "IX"   => 9;
        from_roman _      => raise exception FAIL "Unsupported Roman number";
    end;

    printf "from_roman III = %d\n" (from_roman "III");

    linux$ ./my-script
    from_roman III = 3
\end{verbatim}

This facility is particularly useful when writing short 
recursive functions with separate terminal and recursion 
cases:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = [ 1, 2, 3 ];

    fun sum_list ([],       sum) => sum;
        sum_list (i ! rest, sum) => sum_list (rest, sum + i);
    end;

    printf "%d-element list summing to %d.\n" (list::length r) (sum_list (r, 0));

    linux$ ./my-script
    3-element list summing to 6.
\end{verbatim}


\cutend*



% --------------------------------------------------------------------------------
\subsection{Anonymous Functions}
\cutdef*{subsubsection}
\label{section:ref:functions:anonymous-functions}

Mythryl makes it easy to write anonymous functions.
The basic syntax is:

\begin{quotation}
~~~~fn {\it arg} = {\it expression} 
\end{quotation}

Such functions are typically 
passed as arguments to other functions:

\begin{verbatim}
    linux$ my

    eval: map  (fn string = implode (reverse (explode string)))  [ "abc", "def", "ghi" ];

    ["cba", "fed", "ihg"]
\end{verbatim}

Like named functions, Mythryl anonymous functions support implicit case 
statements.  The general syntax is

\begin{quotation}
~~~~fn {\it pattern} => {\it expression}; \newline
~~~~   {\it pattern} => {\it expression};  \newline
~~~~   {\it pattern} => {\it expression}; \newline
~~~~   ... \newline
~~~~end
\end{quotation}

Arbitrary pattern-matching may be done, but ordinarily if you 
are going to write many cases with complex patterns and  
expressions, you will probably just make it a named function.

Here is a simple example of using this facility to special-case empty strings:

\begin{verbatim}
    linux$ my

    eval: map  fn "" => "<empty>"; string => implode (reverse (explode string)); end  [ "abc", "def", "ghi", "" ];

    ["cba", "fed", "ihg", "<empty>"]
\end{verbatim}


\cutend*


% --------------------------------------------------------------------------------
\subsection{Thunk Syntax}
\cutdef*{subsubsection}
\label{section:ref:functions:thunk-syntax}

A function which takes only {\tt Void} as an argument  
is often called a {\it thunk}.  (Legend has it that 
the name comes from Algol 68, in which they were 
used to implement call-by-name, the explanation 
being that that the called routine didn't have to 
think about the expression because the compiler had 
already "thunk" about it.)

Such functions are 
often used to encapsulate suspended computations which 
may be passed around or stored in datastructures and 
then later continued by invoking them with a void 
argument.

Thunks may be written easily enough using vanilla 
Mythryl anonymous function syntax:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    thunk =   fn () = print "Done!\n";

    thunk ();

    linux$ ./my-script
    Done!
\end{verbatim}

There are however times when even the above syntax can be 
annoying verbose;  the {\tt fun () =} prefix is visually 
distracting from the actual computation to be performed.

For such times Mythryl provides a special {\it thunk notation}, 
which looks just like a code block with a leading dot:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    thunk =   .{ print "Done!\n"; };

    thunk ();

    linux$ ./my-script
    Done!
\end{verbatim}

This syntax is entirely equivalent to the preceding 
anonymous function syntax, but is more compact and 
less distracting.

Mythryl thunk syntax does also support arguments.

Suppose for example that you wish to drop all nines 
from a list of integers.  Package {\tt list} provides 
a function {\tt list::filter} which accepts a predicate 
function and a list and drops all list elements not 
satisfying the predicate function, which will do the 
job nicely:

\begin{verbatim}
    linux$ my

    eval: filter  (fn a = a != 9)  [ 1, 9, 2, 4, 9, 9 ];

    [1, 2, 4]
\end{verbatim}

The anonymous function syntax is however visually distracting 
here.  Thunk syntax lets us do better:

\begin{verbatim}
    linux$ my

    eval:  filter  .{ #a != 9; }  [ 1, 9, 2, 4, 9, 9 ];

    [1, 2, 4]
\end{verbatim}

Here the \# symbol marks the argument.  This syntax is 
distinctly more readable than the vanilla anonymous function 
syntax.

Thunk syntax also supports multiple arguments, although in 
general if you need multiple arguments you should probably 
be writing a regular anonymous or named function.  One 
nice application however is comparison function arguments 
to sort functions.  For example the {\tt list\_mergesort::sort} 
function sorts a list according to a supplied comparison 
function.  Suppose we wish to sort a list of strings by length:

\begin{verbatim}
    linux$ my

    eval:  list_mergesort::sort  (fn (a, b) = strlen(a) > strlen(b))  [ "a", "def", "ab" ]; 

    ["a", "ab", "def"]
\end{verbatim}

This works fine, but again the anonymous-function syntax is 
somewhat distracting.  Thunk syntax is more concise and readable:

\begin{verbatim}
    linux$ my

    eval:  list_mergesort::sort  .{ strlen(#a) > strlen(#b); }  [ "a", "def", "ab" ]; 

    ["a", "ab", "def"]
\end{verbatim}

Thunk syntax is particularly useful when writing functions which 
are intended to mimic the functionality of compiler-implemented 
control structures:

\begin{verbatim}
    #!/usr/bin/mythryl

    foreach [ "abc", "def", "ghi" ] .{
        printf "%s\n" #word;
    };

    linux$ ./my-script
    abc
    def
    ghi
\end{verbatim}

Here {\tt foreach} is just a library function accepting two 
arguments:

\begin{verbatim}
    fun foreach []         thunk =>  ();
        foreach (a ! rest) thunk =>  { thunk(a);   foreach rest thunk; };
    end;
\end{verbatim}

By using thunk syntax for the second argument we gain much of the compactness 
and convenience of a control construct built into the compiler.

\cutend*



% --------------------------------------------------------------------------------
\subsection{Curried Functions}
\cutdef*{subsubsection}
\label{section:ref:functions:curried-functions}

Here is another case in which Mythryl functions appear to be taking 
more than one argument:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun join_strings_with_space  string_1  string_2
        =
        string_1 + " " + string_2;

    printf "join_strings_with_space \"abc\" \"def\" = \"%s\".\n" (join_strings_with_space "abc" "def");

    linux$ ./my-script
    join_strings_with_space "abc" "def" = "abc def".
\end{verbatim}

Formally, we still have here functions which accept a single 
argument and return a single result.  What is happening here 
formally is that we have two functions, the first of which 
accepts the {\tt string\_1} argument and which then returns 
the second function, which accepts the {\tt string\_2} argument 
and generates the final result.

The above code is in fact a shorthand for:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun join_strings_with_space  string_1
        =
        fn  string_2
            =
            string_1 + " " + string_2;

    printf "join_strings_with_space \"abc\" \"def\" = \"%s\".\n" (join_strings_with_space "abc" "def");

    linux$ ./my-script
    join_strings_with_space "abc" "def" = "abc def".
\end{verbatim}

That this is more than a polite theoretical fiction is 
demonstrated by the fact that we can {\it partially apply} 
curried functions to actually obtain and use the intermediate 
anonymous functions:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun join_strings_with_space  string_1  string_2
        =
        string_1 + " " + string_2;

    prefix_with_abc = join_strings_with_space "abc";
    prefix_with_xyz = join_strings_with_space "xyz";

    printf "Prefixed with abc: \"%s\".\n" (prefix_with_abc "mno");
    printf "Prefixed with xyz: \"%s\".\n" (prefix_with_xyz "mno");

    linux$ ./my-script
    Prefixed with abc: "abc mno".
    Prefixed with xyz: "xyz mno".
\end{verbatim}

For a more extended example of using partial application of 
curried functions, see the 
\ahrefloc{section:tut:fullmonte:parsing-combinators-i}{parsing combinators tutorial}.


\cutend*


% --------------------------------------------------------------------------------
\subsection{Value Capture by Functions}
\cutdef*{subsubsection}
\label{section:ref:functions:value-capture-by-functions}

One property which makes Mythryl functions more generally useful than the 
functions of (say) C is that Mythryl functions can capture values from 
their environment.  This makes it cheap and easy to create specialized 
new functions on the fly at runtime.

Consider a function {\tt increment} which adds one to a given argument:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun increment i
        =
        i + 1;

    printf "Increment %d = %d\n" 1 (increment 1);

    linux$ ./my-script
    Increment 1 = 2
\end{verbatim}

More useful would be a function which can bump its argument up 
by any given amount needed, selectable at runtime.

One way to write such a function is by using currying:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun bump_by_k k i
        =
        i + k;

    bump_by_3  =  bump_by_k  3;
    bump_by_7  =  bump_by_k  7;

    printf "bump_by_3 1 = %d\n" (bump_by_3 1);
    printf "bump_by_7 1 = %d\n" (bump_by_7 1);

    linux$ ./my-script
    bump_by_3 1 = 4
    bump_by_7 1 = 8
\end{verbatim}

But another way is to have an anonymous function capture a 
value from its lexical environment:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun make_bump k
        =
        fn i = i + k;

    bump_by_3  =  make_bump  3;
    bump_by_7  =  make_bump  7;

    printf "bump_by_3 1 = %d\n" (bump_by_3 1);
    printf "bump_by_7 1 = %d\n" (bump_by_7 1);

    linux$ ./my-script
    bump_by_3 1 = 4
    bump_by_7 1 = 8
\end{verbatim}

Here the anonymous function is capturing the value {\tt k} 
present in its lexical environment and remembering it for 
later use. 

Functions which have captured values in this way are 
often termed {\it closures}.

This value capture technique is often very convenient when 
constructing a fate of some sort in the middle of 
a large function with many relevant values in scope.

For another example of the usefulness of this technique see the 
\ahrefloc{section:tut:delving-deeper:roll-your-own-oop}{Roll-Your-Own Object Oriented Programming tutorial}.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Mutually Recursive Functions}
\cutdef*{subsubsection}
\label{section:ref:functions:mutuall-recursive-functions}

Normally the Mythryl compiler processes the lines of each input file 
in reading order, left-to-right, top-to-bottom.  Text as yet unseen 
has no effect upon the meaning of a statement.  This semantics is a 
good match to what the human reader does, thus enhancing readability, 
and  also supports interactive use by allowing Mythryl to evaluate 
expressions one by one as they are entered, rather than needing to 
see the entire input file before any output can be generated.

However, this semantics does not suffice for mutually recursive 
functions;  somehow the compiler must be told that the first 
function in a mutually recursive set refers to as-yet unseen 
functions coming up, and thus that the compiler must postpone 
analysis until the complete set of mutually recursive functions 
has been read.

To do this Mythryl uses the reserved word {\it also}:  Instead 
of closing the first mutually recursive function with a semicolon, 
it is connected to the next mutually recursive function with {\it also}.

As a simple contrived example, suppose that we wish to compute 
whether a list is of even or odd length using mutually recursive 
functions.  A simpler way to do this would be to just use the 
existing {\tt list::length} function and test the low bit of 
the result:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun list_length_is_odd  some_list
        =
        ((list::length some_list) & 1) == 1;

    printf "list_length_is_odd [1, 2] = %s\n"    (list_length_is_odd [1, 2]    ?? "TRUE" :: "FALSE");
    printf "list_length_is_odd [1, 2, 3] = %s\n" (list_length_is_odd [1, 2, 3] ?? "TRUE" :: "FALSE");

    linux$ ./my-script
    list_length_is_odd [1, 2] = FALSE
    list_length_is_odd [1, 2, 3] = TRUE
\end{verbatim}

But we want to do it the hard way:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    fun list_length_is_odd  some_list
        =
        even_helper some_list
        where
            fun even_helper []          => FALSE;
                even_helper (a ! rest)  =>  odd_helper rest;
            end

            also
            fun  odd_helper []          => TRUE;
                 odd_helper (a ! rest)  => even_helper rest;
            end;
        end;

    printf "list_length_is_odd [1, 2] = %s\n"    (list_length_is_odd [1, 2]    ?? "TRUE" :: "FALSE");
    printf "list_length_is_odd [1, 2, 3] = %s\n" (list_length_is_odd [1, 2, 3] ?? "TRUE" :: "FALSE");

    linux$ ./my-script
    list_length_is_odd [1, 2] = FALSE
    list_length_is_odd [1, 2, 3] = TRUE
\end{verbatim}

Here our two mutually recursive helper functions {\tt even\_helper} and {\tt odd\_helper} 
remember whether we have currently seen an even or odd number of nodes in the list, 
according to which is currently executing, and respectively return {\tt FALSE} or {\tt TRUE} 
when the end of the list is reached.

To signal the mutual recursion, {\tt even\_helper} lacks a terminal semicolon, instead 
being linked to {\tt odd\_helper} by the {\tt also} reserved word.

\cutend*

