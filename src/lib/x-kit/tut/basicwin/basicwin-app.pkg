## basicwin-app.pkg

# Compiled by:
#     src/lib/x-kit/tut/basicwin/basicwin-app.lib

stipulate
    include package   threadkit;				# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
    package cmd =  commandline;					# commandline			is from   src/lib/std/commandline.pkg
    #
    package g2d =  geometry2d;					# geometry2d			is from   src/lib/std/2d/geometry2d.pkg
    package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
    #
#   package tsc =  thread_scheduler_control;			# thread_scheduler_control	is from   src/lib/src/lib/thread-kit/src/posix/thread-scheduler-control.pkg	
    package ib  =  icon_bitmap;					# icon_bitmap			is from   src/lib/x-kit/tut/basicwin/icon-bitmap.pkg
herein

    package basicwin_app: (weak) api {
	#
	do_it' : (List( String ), String, multiword_int::Int) -> winix__premicrothread::process::Status;
	do_it:   String -> winix__premicrothread::process::Status;
	main:   (String, List( String )) -> winix__premicrothread::process::Status;

    }{
								# thread_scheduler_control_g	is from   src/lib/src/lib/thread-kit/src/glue/thread-scheduler-control-g.pkg
	min_wid = 300;
	min_ht  = 200;

	min_sz = { wide => min_wid, high => min_ht };

	# A trace module for debugging output (see threadkit manual) 
	#
	tracing  = logger::make_logtree_leaf { parent => xlogger::xkit_logging, name => "basic_win::tracing", default => FALSE };

	fun log_if f =   logger::log_if  tracing 0 f;

	fun init display_name
	    =
	    {   log_if {. "open display " + display_name; };

		my  ( xdisplay,								# Typically from $DISPLAY environment variable.
		      xauthentication:  Null_Or(xc::Xauthentication)			# Typically from ~/.Xauthority
		    )
		    =
		    xc::get_xdisplay_string_and_xauthentication  NULL /* display_or_null */;

# XXX BUGGO FIXME need xauthentication here: (quick half-fix installed ~ 2011-10-03)
		xsession =   xc::open_xsession (display_name, xauthentication)
			    except
				xc::XSERVER_CONNECT_ERROR s
				    =
				    {   fil::print s;
					fil::print "\n";

					shut_down_thread_scheduler
					#	
					    winix__premicrothread::process::failure;
				    };

		screen = xc::default_screen_of  xsession;

		window_size
		    =
		    {   my  { wide, high }
			    =
			    xc::size_of_screen screen;

			{ wide => wide / 3,
			  high => high / 4
			};
		    };

									    # xwindow	is from   src/lib/x-kit/xclient/xwindow.pkg
		my (window, in_dict, delete_slot)
		    =
		    xc::make_simple_top_window
			screen
			{
			  border_color     =>  xc::black,
			  background_color =>  xc::rgb8_white,
			  #	
			  site  =>   { upperleft    =>  {  col => 0,  row => 0  },
				       size         =>  window_size,
				       border_thickness =>  1
				     }: g2d::Window_Site
			};


		# The real basicwin gets the
		# list of icon sizes for the
		# xsession here.


		icon_ro_pixmap = xc::make_readonly_pixmap_from_clientside_pixmap
				#
				screen
				#
				ib::icon_bitmap;

		log_if {. "set properties"; };

									    # iccc		is from   src/lib/x-kit/xclient/src/api/xkit-lib-external-interface.pkg
									    # commandline	is from   src/lib/std/commandline.pkg
		xc::set_window_manager_properties
		    window
		      {
			window_name => THE "Basic Window Program",
			icon_name   => THE "basicwin",

			size_hints  => [ xc::HINT_PPOSITION,
					 xc::HINT_PSIZE,
					 xc::HINT_PMIN_SIZE min_sz
				       ],

			nonsize_hints => [ xc::HINT_ICON_RO_PIXMAP  icon_ro_pixmap ],
			class_hints   => THE { resource_name => "basicwin", resource_class => "Basicwin" },

			commandline_arguments => cmd::get_commandline_arguments ()
		      };

		xc::show_window  window;

		(xsession, screen, in_dict, window);
	    };

									    # drawing		is from   src/lib/x-kit/xclient/src/api/xkit-lib-external-interface.pkg
									    # PV_FOREGROUND	def in    src/lib/x-kit/xclient/src/window/pen-old.pkg
	fun make_pen screen
	    =
	    xc::make_pen
	      [
		xc::p::FOREGROUND   xc::rgb8_black,
		xc::p::LINE_WIDTH   6,
		xc::p::LINE_STYLE_ON_OFF_DASH,
		xc::p::CAP_STYLE_ROUND,
		xc::p::JOIN_STYLE_ROUND,
		xc::p::DASH_OFFSET  0,
		xc::p::DASH_LIST    [12, 24]
	      ];

									    # font		is from   src/lib/x-kit/xclient/src/api/xkit-lib-external-interface.pkg
	fun load_font display
	    =
	    xc::open_font display "9x15";

	fun place_text (window, pen, font, { wide, high } )
	    =
	    {   log_if {. "place_text"; };

		draw_transparent_string
		    =
		    xc::draw_transparent_string
			(xc::drawable_of_window  window)
			pen
			font;

		text_width = xc::text_width font;

		my (font_high, font_descent)
		    =
		    {   my { ascent, descent }
			    =
			    xc::font_high font;

			(ascent + descent, descent);
		    };

		fun draw (y_pos, s)
		    =
		    {   w = text_width s;

			draw_transparent_string
			  ( { col => ((wide - w) / 2),
			      row => y_pos
			    },
			    s
			  );
		    };

		y_offset = (high / 2) - font_high - font_descent;

		my  { wide => scr_wid, high => scr_ht }
		    =
		    xc::size_of_screen (xc::screen_of_window  window);

		depth = xc::depth_of_screen (xc::screen_of_window  window);

		log_if {. "place_text: draw text"; };

		apply
		    draw
		    [
		      (font_high,			"Hi! I'm a window, who are you?"),
		      (high - (2*font_high),	"To terminate program: press any key"),
		      (y_offset,			"Screen Dimensions:"),
		      (y_offset + (1*font_high),	" Height - " + (int::to_string scr_ht)  + " pixels"),
		      (y_offset + (2*font_high),	" Width  - " + (int::to_string scr_wid) + " pixels"),
		      (y_offset + (3*font_high),	" Depth  - " + (int::to_string depth)   + " plane (s)"),
		      (high - font_high,		"or button while in this window.")
		    ];
	    };

	fun place_graphics (window, pen, { wide => win_wid, high => win_ht } )
	    =
	    {   log_if {. "place_graphics"; };

		wide = (3 * win_wid) / 4;
		high = win_ht / 2;

		xc::draw_box
		    #
		    (xc::drawable_of_window  window)
		    #
		    pen
		    #
		    ( { col => (win_wid / 2) - (wide / 2),
			row => (win_ht  / 2) - (high / 2),
			wide,
			high
		      }
		    );
	    };

	fun too_small (window, pen, font)
	    =
	    {   my { ascent, ... }
		    =
		    xc::font_high font;

		xc::draw_transparent_string
		    #
		    (xc::drawable_of_window  window)
		    pen
		    font;

		( { col => 2, row => ascent+2 },
		  "Too Small"
		);
	    };

	fun basicwin display_name
	    =
	    {   log_if {. "basicwin init"; };

		(init display_name)
		    ->
		    (xsession, screen, xc::KIDPLUG { from_mouse', from_keyboard', from_other', ... }, window);

		from_mouse'    =  from_mouse'    ==>  xc::get_contents_of_envelope;
		from_keyboard' =  from_keyboard' ==>  xc::get_contents_of_envelope;
		from_other'    =  from_other'    ==>  xc::get_contents_of_envelope;

		log_if {. "basicwin make_pen"; };

		pen =  make_pen  screen;

		log_if {. "load"; };

		font = load_font xsession;


		fun quit _
		    =
		    {   log_if {. "QUIT"; };

			xc::close_xsession xsession;

			shut_down_thread_scheduler  winix__premicrothread::process::success;
		    };


		fun size_too_small ({ wide, high } )
		    =
		    (wide < min_wid) or (high < min_ht);


										    # window_dictionary	is from   src/lib/x-kit/xclient/src/window/window-dictionary.pkg
										    # MOUSE_FIRST_DOWN	def in    src/lib/x-kit/xclient/src/window/window-dictionary.pkg
										    # ETC_RESIZE	def in    src/lib/x-kit/xclient/src/window/window-dictionary.pkg
		fun loop size
		    =
		    {   fun do_mouse (xc::MOUSE_FIRST_DOWN _) =>  quit ();
			    do_mouse (xc::MOUSE_LAST_UP _)    =>  quit ();
			    do_mouse _                        =>  loop size;
			end;


			fun do_mom (xc::ETC_RESIZE ({ wide, high, ... } ))
				=>
				loop ({ wide, high } );

			    do_mom (xc::ETC_REDRAW _)
				=>
				{	if (size_too_small size)
					#
					too_small (window, pen, font);
					();
				    else
					place_text     (window, pen, font, size);
					place_graphics (window, pen, size);
				    fi;

				    loop size;
				};

			    do_mom  xc::ETC_OWN_DEATH
				=>
				quit ();

			    do_mom (xc::ETC_CHILD_BIRTH _ | xc::ETC_CHILD_DEATH _) =>   raise exception FAIL "Bug: Unsupported case in do_mom.";
			end;


			do_one_mailop [
			    from_mouse'    ==> do_mouse,
			    from_keyboard' ==> quit,
			    from_other'    ==> do_mom
			];
		    };


		log_if {. "go"; };

		loop min_sz;
	    };

	fun do_it' (flgs, xsession, tq)
	    =
	    {   xlogger::init flgs;

		start_up_thread_scheduler'
		    #
		    (time::from_milliseconds tq)
		    #
		    (\\ ()  =   {   xlogger::make_thread  "basicwin"   {. basicwin xsession; };
				    ();
				}
		    );
	    };

	fun do_it s
	    =
	    do_it' ([], s, 20);

	fun main (program, "-display" ! server ! _) =>  basicwin server;
	    main _                                  =>  basicwin "";
	end;
    };						# package basic_win

end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## This code was inspired by a C program that is under the following copyright:
## Copyright 1989 O'Reilly and Associates, Inc.
## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2014,
## released per terms of SMLNJ-COPYRIGHT.
