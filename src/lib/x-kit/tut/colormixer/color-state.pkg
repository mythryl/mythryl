## color-state.pkg
#
# A very simple package implementing Color_State imps
# which remember their RGB color, accept requests to
# change one or all of the RGB color components, and
# report each change to an output mailslot.

# Compiled by:
#     src/lib/x-kit/tut/colormixer/colormixer-app.lib

stipulate
    include threadkit;				# threadkit	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xc  =  xclient;			# xclient	is from   src/lib/x-kit/xclient/xclient.pkg
herein
 
    package   color_state
    :         Color_State			# Color_State	is from   src/lib/x-kit/tut/colormixer/color-state.api
    {
	Rgb = xc::Rgb;

	Plea_Mail
	  #
	  = CHANGE_R    Unt
	  | CHANGE_G    Unt
	  | CHANGE_B    Unt
	  | CHANGE_RGB  Rgb
	  ;

	Color_State
	    = 
	    COLOR_STATE (Mailslot( Plea_Mail ), Mailslot( Rgb ) );

	fun make_color_state  initial_color
	    =
	    {   plea_slot =  make_mailslot ();
		out_slot  =  make_mailslot ();

		fun get_plea ()      =  take plea_slot;
		fun send_color color =  give (out_slot, color);

		fun change_color (rgb, plea)
		    =
		    {   (xc::rgb_to_unts rgb)
			    ->
			    (r, g, b);

			new_color
			    =
			    case plea
				#
				CHANGE_R new =>  xc::rgb_from_unts (new, g,   b  );
				CHANGE_G new =>  xc::rgb_from_unts (r,   new, b  );
				CHANGE_B new =>  xc::rgb_from_unts (r,   g,   new);
				#
				CHANGE_RGB r =>  r;
			    esac;

			send_color  new_color;

			new_color;
		    };

		fun loop color
		    =
		    loop (change_color (color, get_plea ()));


		make_thread "color state"  .{

		    loop initial_color;
		};


		COLOR_STATE (plea_slot, out_slot);
	    };


	fun change_color  (COLOR_STATE (plea_slot, _))  plea
	    =
	    give (plea_slot, plea);


	fun colorchange'_of  (COLOR_STATE(_, out_slot))
	    =
	    take' out_slot;

    };				# package color_state;

end; 
 
## COPYRIGHT (c) 1991, 1995 by AT&T Bell Laboratories.  
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released per terms of SMLNJ-COPYRIGHT.
