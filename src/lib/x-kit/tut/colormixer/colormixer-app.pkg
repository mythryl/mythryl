## colormixer-app.pkg
#
# One way to run this app from the base-directory commandline is:
#
#     linux% my
#     eval: make "src/lib/x-kit/tut/colormixer/colormixer-app.lib";
#     eval: colormixer_app::do_it ();

# Compiled by:
#     src/lib/x-kit/tut/colormixer/colormixer-app.lib

stipulate
    include threadkit;					# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package f8b =  eight_byte_float;			# eight_byte_float		is from   src/lib/std/eight-byte-float.pkg
    package xg  =  xgeometry;				# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
    package xc  =  xclient;				# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
    #
    package bdr =  border;				# border			is from   src/lib/x-kit/widget/wrapper/border.pkg
    package sld =  slider;				# slider			is from   src/lib/x-kit/widget/leaf/slider.pkg
    package lbl =  label;				# label				is from   src/lib/x-kit/widget/leaf/label.pkg
    package top =  topwindow;				# topwindow			is from   src/lib/x-kit/widget/basic/topwindow.pkg
    package rw  =  root_window;				# root_window			is from   src/lib/x-kit/widget/basic/root-window.pkg
    package rx  =  run_in_x_window;			# run_in_x_window		is from   src/lib/x-kit/widget/lib/run-in-x-window.pkg
    package wg  =  widget;				# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa  =  widget_attribute;			# widget_attribute		is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy  =  widget_style;			# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
    #
    package cs  =  color_state;
    package low =  line_of_widgets;			# line_of_widgets		is from   src/lib/x-kit/widget/layout/line-of-widgets.pkg
    package sz  =  size_preference_wrapper;		# size_preference_wrapper	is from   src/lib/x-kit/widget/wrapper/size-preference-wrapper.pkg
    package tgl =  toggleswitches;			# toggleswitches		is from   src/lib/x-kit/widget/leaf/toggleswitches.pkg
    package ts  =  thread_scheduler;			# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    #
    package xtr =  xlogger;				# xlogger			is from   src/lib/x-kit/xclient/pkg/stuff/xlogger.pkg
    #
    tracefile   =  "colormixer-app.trace.log";
    tracing     =  logger::make_logtree_leaf { parent => xlogger::xkit_logging, name => "mixer_app::tracing" };
    trace	=  xtr::log_if  tracing 0;		# Conditionally write strings to tracing.log or whatever.
        #
        # To debug via tracelogging, annotate the code with lines like
	#
        #	trace .{ sprintf "foo/top: bar d=%d" bar; };
	#
	# and then set   write_tracelog = TRUE;   below.
herein

    package colormixer_app: api {

	do_it':  (List( String ), String) -> Void;
	do_it:   Void -> Void;
	main:    (List(String), X) -> Void;

	selfcheck:  Void -> { passed: Int,
			      failed: Int
			    };
    }{
	write_tracelog = FALSE;

	app_task		   =  REF (NULL: Null_Or( Apptask   ));

	run_selfcheck		   =  REF FALSE;

	stipulate
	    selfcheck_tests_passed =  REF 0;
	    selfcheck_tests_failed =  REF 0;
	herein
	    fun reset_global_mutable_state ()					# Reset above state variables to load-time values.
		=									# This will be needed if (say) we get run multiple times interactively without being reloaded.
		{   run_selfcheck		:=  FALSE;
		    #
		    app_task			:=  NULL;
		    #
		    selfcheck_tests_passed	:=  0;
		    selfcheck_tests_failed	:=  0;
		};

	    fun test_passed () =  selfcheck_tests_passed :=  *selfcheck_tests_passed + 1;
	    fun test_failed () =  selfcheck_tests_failed :=  *selfcheck_tests_failed + 1;
	    #
	    fun assert bool    =  if bool   test_passed ();
				  else      test_failed ();
				  fi;				
	    #
	    fun test_stats  ()
		=
                { passed => *selfcheck_tests_passed,
                  failed => *selfcheck_tests_failed
                };


	    fun kill_colormixer_app ()
		=
		{
log::note .{ sprintf "%s\tkill_colormixer_app/TOP:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
		    kill_task  { success => TRUE,  task => (the *app_task) };
log::note .{ sprintf "%s\tkill_colormixer_app/ZZZ:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
		};

	    fun wait_for_app_task_done ()
		=
		{
log::note .{ sprintf "%s\twait_for_app_task_done/TOP  -- colormixer-app.pkg"  (ts::thread_scheduler_statestring ()); };
		    task =  the  *app_task;
		    #
log::note .{ sprintf "%s\twait_for_app_task_done/BBB  -- colormixer-app.pkg"  (ts::thread_scheduler_statestring ()); };
		    task_finished' =  task_done__mailop  task;

log::note .{ sprintf "%s\twait_for_app_task_done/CCC  -- colormixer-app.pkg"  (ts::thread_scheduler_statestring ()); };
		    block_until_mailop_fires  task_finished';

log::note .{ sprintf "%s\twait_for_app_task_done/DDD  -- colormixer-app.pkg"  (ts::thread_scheduler_statestring ()); };
		    assert (get_task's_state  task  ==  state::SUCCESS);
log::note .{ sprintf "%s\twait_for_app_task_done/ZZZ  -- colormixer-app.pkg"  (ts::thread_scheduler_statestring ()); };
		};


	end;

	resources = ["*background: gray"];

	maxcolor =  0u65535;
	midcolor =  maxcolor / 0u2;
	mincolor =  0u0;

	border_thickness =    4;

	slider_width     =   20;
	hue_box_dim      =   25;

	big_spot_height  =  400;
	big_spot_width   =  150;

	horizontal_spacer =  low::SPACER { min_size=>5,  ideal_size=>5, max_size=>THE 5 };
	vertical_spacer   =  low::SPACER { min_size=>1,  ideal_size=>5, max_size=>NULL  };

	pause = time::from_milliseconds 500;

	redc   = xc::rgb_from_unts (midcolor, 0u0,      0u0      );
	greenc = xc::rgb_from_unts (0u0,      midcolor, 0u0      );
	bluec  = xc::rgb_from_unts (0u0,      0u0,      midcolor );
	blackc = xc::rgb_from_unts (0u0,      0u0,      0u0      );

	fun make_red   n =  xc::rgb_from_unts (n,        mincolor, mincolor );
	fun make_green n =  xc::rgb_from_unts (mincolor, n,        mincolor );
	fun make_blue  n =  xc::rgb_from_unts (mincolor, mincolor, n        );

	fun make_mixer (root_window, view)
	    =
	    {   white =  xc::white;

		selfcheck_colorchange_watcher
                    =
                    REF (NULL:  Null_Or( Mailqueue( xc::Rgb ) ));

		fun quit ()
		    =
		    {   fun q ()
			    =
			    block_until_mailop_fires (timeout_in' pause); 

			rw::delete_root_window  root_window; 

#			shut_down_thread_scheduler  winix::process::success;

			make_thread "mixer" q;			# This code makes no sense to me.

			();
		    };

		switch = tgl::make_rocker_toggleswitch'
			     (root_window, view,[])
			     (fn _ = quit ());

		switch_line
		    =
		    low::HZ_CENTER
		      [
			vertical_spacer,
			low::WIDGET (tgl::as_widget switch),
			horizontal_spacer
		      ];

		fun make_display_box  color  w
		    =
		    {   args = [ (wa::background,       wa::COLOR_VAL  color),
				 (wa::border_thickness, wa::INT_VAL  border_thickness)
			       ];

			display
			    =
			    bdr::border
				(root_window, view, args)
				(sz::make_tight_size_preference_wrapper w);

			low::HZ_CENTER
			  [ vertical_spacer,
			    low::WIDGET (bdr::as_widget display),
			    vertical_spacer
			  ];
		    };

		fun paint_spot spot color
		    = 
		    spot::set_spot spot color
		    except
			_ = {   file::print "out of color cells\n";
				quit();
			    };

		spot = spot::spot
			 (root_window, view) 
			 { color => blackc,
			   high  => big_spot_height,
			   wide  => big_spot_width
			 };

		paint =  paint_spot  spot;

		color_screen
		    =
		    make_display_box  white  (spot::as_widget spot);

		colorstate   =  cs::make_color_state blackc;
		change_color =  cs::change_color     colorstate;
		colorchange' =  cs::colorchange'_of  colorstate;


		fun paint_loop ()
		    =
		    for (;;) {
			#
			new_color =  block_until_mailop_fires  colorchange';
		        paint new_color;
			#
			case *selfcheck_colorchange_watcher
			    #
			    THE mailqueue
				=>
				push (mailqueue, new_color);

			    NULL => ();
			esac;
		    };


		# Construct a control row consisting of
		#
		#  o A color patch.
		#  o A slider.
		#  o A numeric readout.
		#
		# The colormixer app uses one such
		# control row each for red, green and blue:
		#
		fun make_color_control_row
                        rgb						# One of:  redc, greenc, bluec.
                        make_color					# One of:  make_red, make_green, make_blue.
                        mkmsg						# One of:  cs::CHANGE_R, cs::CHANGE_G, cs::CHANGE_B.
		    =
		    (line, printer_loop, slider)			# Slider is returned only for selfcheck support.
		    where
			(xc::rgb_to_unts rgb)
			    ->
			    (red, green, blue);

			rgb_color = xc::get_color (xc::CMS_RGB { red, green, blue });

			l_args = [ (wa::label,      wa::STRING_VAL "          0"),
				   (wa::background, wa::COLOR_VAL  rgb_color)
				 ];

			label = lbl::make_label' (root_window, view, l_args);

			display = make_display_box rgb_color (lbl::as_widget label);

			s_args = [ (wa::is_vertical, wa::BOOL_VAL FALSE),
				   (wa::background,  wa::STRING_VAL "gray"),
				   (wa::width,       wa::INT_VAL slider_width),
				   (wa::from_value,  wa::INT_VAL 0),
				   (wa::to_value,    wa::INT_VAL (unt::to_int_x maxcolor))
				 ];

			slider = sld::make_slider (root_window, view, s_args);

			spot = spot::spot
				 (root_window, view) 
				 { color => blackc,
				   high  => hue_box_dim,
				   wide  => hue_box_dim
				 };

			hue_box = make_display_box white (spot::as_widget spot);

			line = low::HZ_CENTER
				 [
				   horizontal_spacer, 
				   hue_box, 
				   horizontal_spacer, 
				   low::WIDGET (sld::as_widget slider), 
				   horizontal_spacer,
				   display, 
				   horizontal_spacer
				 ];

			set = lbl::set_label label;

			slider_motion'
			    =
			    sld::slider_motion'_of  slider
				==>
				unt::from_int;

			paint = paint_spot spot;

			fun printer_loop ()
			    =
			    loop 0u0
			    where
				# The first loop is just to
				# initialize the display;
				# subsequent loops respond to
				# user mouse motions:
				# 
				fun loop n
				    =	
				    {   set (lbl::TEXT (unt::format number_string::DECIMAL n));

					paint (make_color n);

					change_color (mkmsg n);

					loop (block_until_mailop_fires  slider_motion'); 
				    };
			    end;	
		    end;

		my (red_line,   red_printer_loop,     red_slider) =  make_color_control_row  redc    make_red    cs::CHANGE_R;
		my (green_line, green_printer_loop, green_slider) =  make_color_control_row  greenc  make_green  cs::CHANGE_G;
		my (blue_line,  blue_printer_loop,   blue_slider) =  make_color_control_row  bluec   make_blue   cs::CHANGE_B;


		make_thread "mixer red"      red_printer_loop; 
		make_thread "mixer green"    green_printer_loop;
		make_thread "mixer blue"     blue_printer_loop ;
		make_thread "mixer painter"  paint_loop;

		( low::as_widget
		      (low::make_line_of_widgets  root_window
			  (low::VT_CENTER
			    [			vertical_spacer,
			      color_screen,	vertical_spacer,        
			      switch_line,	vertical_spacer,
			      red_line,		vertical_spacer,
			      green_line,	vertical_spacer,
			      blue_line,	vertical_spacer
			    ]
		      )   ),
		  { red_slider, green_slider, blue_slider, selfcheck_colorchange_watcher }
                );
	    };				# fun make_mixer 


	# Thread to exercise the app by simulating user
	# mouse-drags of the colormixer sliders and
	# verifying their effects:
	#
	fun make_selfcheck_thread  { topwindow, widgettree, selfcheck_api => { red_slider, green_slider, blue_slider, selfcheck_colorchange_watcher } }
	    =
	    xtr::make_thread "colormixer-app selfcheck'" selfcheck'
	    where

		# Convert a pair of 0.0 -> 1.0 window X coordinates into
		# a corresponding series of pixel-coordinate points:
		#
		fun window_x_points (window, start, stop)
		    =
		    {
			# Get size of slider window:
			#
			(xc::get_window_site  window)
			    ->
			    xg::BOX { row, col, high, wide };

			start_col =  f8b::round ((f8b::from_int wide) * start);
			stop_col  =  f8b::round ((f8b::from_int wide) *  stop);

			cols = (start_col + 1)..(stop_col - 1);
			row  = row + high/2;

			fun col_to_point col
			    =
			    xg::POINT { col, row };

			( col_to_point     start_col,
			  map col_to_point cols,
			  col_to_point     stop_col
			);
		    };

		fun slider_window  slider
		    =
		    {   widget = slider::as_widget  slider;
			#
			window = widget::window_of  widget;

			window;
		    };	

		fun slider_site  slider
		    =
		    xc::get_window_site  (slider_window slider);		# -> xg::BOX { row, col, high, wide };

		# Simulate a mousedrag of slider
		#
		fun drag_slider  (slider, start, stop)				# Start, stop are floats in range 0.0 -> 1.0
		    =
		    {   button = xc::MOUSEBUTTON 1;
			#
			window = slider_window slider;

			(window_x_points (window, start, stop))
			    ->
			    (start_point, midpoints, stop_point);

			xc::send_mousebutton_press_xevent   { window, button, point => start_point };

			apply drag midpoints
			    where
				fun drag point
				    =
				    {   xc::send_mouse_motion_xevent
					  {
					    window,
					    point,
					    buttons => [ button ]
					  };

					sleep_for (time::from_milliseconds 50);
					    #
					    # Without this sleep we lose events, resulting in the below
					    #     assert (changes == mailqueue_entries);
					    # failing.  I'm ignoring this for now because I am assuming
					    # that this is due to the X server dropping events when they
					    # come too quickly, or possibly an issue in xkit, and for the
					    # moment I'm really only interested in threadkit.  -- 2012-09-15 CrT
				    };
			    end;

			xc::send_mousebutton_release_xevent { window, button, point =>  stop_point };

			(list::length midpoints) + 2;		# Number of color changes.
		    };	

	        fun selfcheck' ()
		    =
		    {   # Wait until the widgettree is realized and running:
			# 
log::note .{ sprintf "%s\tselfcheck'/TOP:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			get (wg::get_''gui_startup_complete''_oneshot_of  widgettree);

log::note .{ sprintf "%s\tselfcheck'/AAA: Sleeping 250ms.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			sleep_for (time::from_milliseconds 250);	# Shouldn't be needed, but preceding doesn't eliminate race conditions as intended... :-(

log::note .{ sprintf "%s\tselfcheck'/BBB: Making mailqueue.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			mailqueue =  make_mailqueue ():   Mailqueue( xc::Rgb );

			selfcheck_colorchange_watcher :=   THE mailqueue;

log::note .{ sprintf "%s\tselfcheck'/CCC: Unpacking sliders.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			(slider_site   red_slider) -> xg::BOX { row =>   red_row, col =>   red_col, high =>   red_high, wide =>   red_wide };
			(slider_site green_slider) -> xg::BOX { row => green_row, col => green_col, high => green_high, wide => green_wide };
			(slider_site  blue_slider) -> xg::BOX { row =>  blue_row, col =>  blue_col, high =>  blue_high, wide =>  blue_wide };

			changes  = 0;
log::note .{ sprintf "%s\t%s\t%d selfcheck'/DDD: Dragging red_slider: changes d=%d   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) changes; };
			changes += drag_slider (  red_slider, 0.4, 0.6);
log::note .{ sprintf "%s\t%s\t%d selfcheck'/EEE: Dragging green_slider: changes d=%d   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) changes; };
			changes += drag_slider (green_slider, 0.4, 0.6);
log::note .{ sprintf "%s\t%s\t%d selfcheck'/FFF: Dragging blue_slider: changes d=%d   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) changes; };
			changes += drag_slider ( blue_slider, 0.4, 0.6);

			mailqueue_entries =  mailqueue_length  mailqueue;

			assert (changes == mailqueue_entries + 2);
			    #
			    # I have no idea where the "+ 2" is coming from above,
			    # but at the moment I'm only interested in exercising
			    # the thread-scheduler and mailops, not in debugging
			    # x-kit much less colormixer-app, so as long as the +2
			    # is consistent I'm happy.  -- 2012-09-15 CrT

log::note .{ sprintf "%s\t%s\t%d selfcheck'/GGG: Emptying color mailqueue: changes d=%d   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) changes; };
			for (i = 0, last_red = 0, last_green = 0, last_blue = 0;  i < mailqueue_entries; ++i) {
			    #
log::note .{ sprintf "%s\t%s\t%d selfcheck'/HHH: color mailqueue loop(%d of %d)...   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ())  (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) i changes; };
			    new_color = pull mailqueue;
			    #
log::note .{ sprintf "%s\t%s\t%d selfcheck'/III: color mailqueue loop(%d of %d)...   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ())   (mailqueue_to_string mailqueue) (mailqueue_length mailqueue) i changes; };
			    (xc::rgb_to_unts new_color) -> (red, green, blue);
			    #
log::note .{ sprintf "%s\tselfcheck'/JJJ: color mailqueue loop(%d of %d)...   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) i changes; };
			    red   =  unt::to_int   red;
			    green =  unt::to_int green;
			    blue  =  unt::to_int  blue;
			    #
log::note .{ sprintf "%s\tselfcheck'/KKK: color mailqueue loop(%d of %d)...   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) i changes; };
			    assert (  red >= last_red  );
			    assert (green >= last_green);
			    assert ( blue >= last_blue );
			    #
log::note .{ sprintf "%s\tselfcheck'/LLL: color mailqueue loop(%d of %d)...   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()) i changes; };
			    last_red   =   red;
			    last_green = green;
			    last_blue  =  blue;
			};

log::note .{ sprintf "%s\tselfcheck'/UUU: Done emptying color mailqueuer.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			selfcheck_colorchange_watcher :=   NULL;

log::note .{ sprintf "%s\tselfcheck'/VVV: Sleeping 250ms.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			sleep_for (time::from_milliseconds  250);



			# All done -- shut everything down:
			#
log::note .{ sprintf "%s\tselfcheck'/WWW: Closing xsession.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			(xc::xsession_of_window  (wg::window_of widgettree)) ->  xsession;
			xc::close_xsession  xsession;

log::note .{ sprintf "%s\tselfcheck'/XXX: Sleeping for 200ms.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			sleep_for (time::from_milliseconds 200);				# I think close_xsession returns before everything has shut down. Need something cleaner here. XXX SUCKO FIXME.

log::note .{ sprintf "%s\tselfcheck'/YYY: Calling kill_colormixer_app.   -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
			kill_colormixer_app ();

#			shut_down_thread_scheduler  winix::process::success;
		    };
	    end;						# fun make_selfcheck_thread

	fun start_up_colormixer_app_threads  root_window
	    =
	    {   style = wg::style_from_strings (root_window, resources);

		name = wy::make_view
			 { name    =>   wy::style_name [],
			   aliases => [ wy::style_name [] ]
			 };
		view = (name, style);

		my (widgettree, selfcheck_api) = make_mixer (root_window, view);

		args =  [ (wa::title,     wa::STRING_VAL "RGB Mixer"),
			  (wa::icon_name, wa::STRING_VAL "MIX")
			];

		topwindow = top::topwindow (root_window, view, args) widgettree;

		top::start  topwindow;

		if *run_selfcheck
		    #
		    make_selfcheck_thread  { topwindow, widgettree, selfcheck_api };
		    ();
		fi;

		();
	    };

	fun set_up_tracing ()
	    =
	    {   # Open tracelog file and select tracing level.
		# We don't need to truncate any existing file
		# because that is already done by the logic in
		#     src/lib/std/src/posix/winix-text-file-io-driver-for-posix.pkg
		#
		include logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
		#
		set_logger_to  (file::LOG_TO_FILE tracefile);
#		enable file::all_logging;				# Gross overkill.
	    };

	fun set_up_colormixer_app_task  root_window
	    =
	    # Here we arrange that all the threads
	    # for the application run as a task "colormixer app",
	    # so that later we can shut them all down with
	    # a simple kill_task().  We explicitly create one
	    # root thread within the task; the rest then implicitly
	    # inherit task membership:
	    #
	    {   colormixer_app_task =   (the *app_task);
		#
		xtr::make_thread' [ THREAD_NAME "colormixer app",
				    THREAD_TASK  colormixer_app_task
				  ]
				  start_up_colormixer_app_threads
				  root_window;
		();
	    };

	fun do_it' (debug_flags, server)
	    =
	    {   xlogger::init debug_flags;
		#
		if write_tracelog   set_up_tracing ();   fi;

		colormixer_app_task =   make_task  "colormixer app"  [];
		app_task           :=   THE  colormixer_app_task;

		rx::run_in_x_window'  set_up_colormixer_app_task  [ rx::DISPLAY server ];

		wait_for_app_task_done ();
	    };


	fun do_it ()
	    =
	    {   if write_tracelog   set_up_tracing ();   fi;
		#
		colormixer_app_task =   make_task  "colormixer app"  [];
		app_task           :=   THE  colormixer_app_task;

		rx::run_in_x_window  set_up_colormixer_app_task;

		wait_for_app_task_done ();
	    };


	fun selfcheck ()
	    =
	    {
log::note .{ sprintf "%s\tselfcheck/TOP:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
		reset_global_mutable_state ();
		run_selfcheck :=  TRUE;
log::note .{ sprintf "%s\tselfcheck/MMM:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
		do_it ();
log::note .{ sprintf "%s\tselfcheck/NNN:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
result =
		test_stats ();
log::note .{ sprintf "%s\tselfcheck/ZZZ:    -- colormixer-app.pkg" (ts::thread_scheduler_statestring ()); };
result;
	    };	


	fun main (program ! server ! _, _)
		=>
		do_it' ([], server);

	    main _
		=>
		do_it ();
	end;
    };				# package colormixer_app
end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1991, 1995 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
