## menu.pkg

# Compiled by:
#     src/lib/x-kit/tut/bouncing-heads/bouncing-heads-app.lib


# This is a very simple menu for the bounce demo; it just returns the selected
# string (or NULL).


stipulate
    include threadkit;					# threadkit	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xg =  xgeometry;				# xgeometry	is from   src/lib/std/2d/xgeometry.pkg
    package xc =  xclient;				# xclient	is from   src/lib/x-kit/xclient/xclient.pkg
herein

    package menu {

	stipulate

	    menu_font_name = "8x13";

	    x_padding = 4;		# Pad four pixels horizontally.

	    menu_items = [ "Refresh", "Kill All", "Quit" ];

	    # Translate mouse events by delta:
	    # 
	    fun translate_mouse delta
		=
		trans
		where
		    fun trans (xc::MOUSE_MOTION { screen_point, timestamp, window_point } )
			    => xc::MOUSE_MOTION { screen_point, timestamp, window_point => xg::point::add (window_point, delta) };

			trans (xc::MOUSE_UP         { mouse_button, screen_point, state, timestamp, window_point } )
			    => xc::MOUSE_UP         { mouse_button, screen_point, state, timestamp, window_point => xg::point::add (window_point, delta) };

			trans (xc::MOUSE_LAST_UP    { mouse_button, screen_point,        timestamp, window_point } )
			    => xc::MOUSE_LAST_UP    { mouse_button, screen_point,        timestamp, window_point => xg::point::add (window_point, delta) };

			trans (xc::MOUSE_DOWN       { mouse_button, screen_point, state, timestamp, window_point } )
			    => xc::MOUSE_DOWN       { mouse_button, screen_point, state, timestamp, window_point => xg::point::add (window_point, delta) };

			trans (xc::MOUSE_FIRST_DOWN { mouse_button, screen_point,        timestamp, window_point } )
			    => xc::MOUSE_FIRST_DOWN { mouse_button, screen_point,        timestamp, window_point => xg::point::add (window_point, delta) };

			trans m
			    =>
			    m;
		    end;
		end;

	    fun place_items (font, items)
		=
		( items,
		  xg::SIZE { wide, high },
		  select_item
		)
		where
		    my { ascent, descent }
			=
			xc::font_high font;

		    txt_wid = xc::text_width font;

		    item_ht = ascent + descent;

		    pad = x_padding + x_padding;


		    fun place ([], cur_y, wid, l)
			    =>
			    (reverse l, wid, cur_y);

			place (s ! r, cur_y, wid, l)
			    =>
			    place
			      ( r,
				cur_y+item_ht,
				int::max (wid, (txt_wid s) + pad),
				(xg::POINT { col=>x_padding, row=>cur_y+ascent }, s) ! l
			      );
		    end;


		    my (items, wide, high)
			=
			place (items, 0, 0, []);


		    fun select_item (pt as xg::POINT { row=>y, ... } )
			=
			if (not (xg::point::in_box (pt, xg::BOX { col => 0, row => 0, wide, high } )))
			    #
			    NULL;
			else
			    i = int::quot (y, item_ht);

			    my (pt as xg::POINT { row=>y, ... }, s)
				=
				list::nth (items, i);

			    THE {
				item     =>  i,
				text_pos =>  pt,
				box      =>  xg::BOX { col=>0, row=>y-ascent, wide, high=>item_ht },
				text     =>  s
			      };
			fi;

		end;

	    # Create and draw the menu window,
	    # returning the window and input dictionary:
	    #
	    fun create_menu screen (xg::POINT { col=>x, row=>y }, pen, font, items)
		=
		{   my (items, menu_sz as xg::SIZE { wide, high }, select_item)
			=
			place_items (font, items);

		    my  xg::SIZE { wide=>scr_wid, high=>scr_ht }
			=
			xc::size_of_screen  screen;

		    menu_origin
			=
			xg::POINT
			  {
			    col => int::min (int::max (x - (wide / 2), 0), scr_wid - wide),
			    row => int::min (int::max (y - (high / 2), 0), scr_ht  - high)
			  };

		    my (menu_win, in_dict)
			=
			xc::make_simple_popup_window  screen
			  {
			    background_color =>  xc::rgb8_white,
			    border_color     =>  xc::black,
			    #
			    site =>  xg::WINDOW_SITE
				       { upperleft    =>  xg::point::subtract (menu_origin, xg::POINT { col=>1, row=>1 } ),
					 size         =>  menu_sz,
					 border_thickness =>  1
				       }
			  };

		    xc::show_window  menu_win;

		    draw_item
			=
			xc::draw_transparent_string  (xc::drawable_of_window  menu_win)  pen  font;

		    map  draw_item  items;		# XXX BUGGO FIXME shouldn't this be 'apply' not 'map'?

		    ( menu_origin,
                      menu_win,
                      xc::ignore_all  in_dict,
                      select_item
                    );
		};
	herein

	    fun popup_menu window
		=
		do_menu
		where
		    screen   =  xc::screen_of_window    window;
		    xsession =  xc::xsession_of_window  window;

		    font = xc::open_font  xsession  menu_font_name;

		    (xc::font_high  font) ->   { ascent, ... };

		    fore_pen = xc::make_pen [xc::p::FOREGROUND  xc::rgb8_black ];
		    back_pen = xc::make_pen [xc::p::FOREGROUND  xc::rgb8_white ];

		    create_menu' =  create_menu screen;

		    menu_cursor
			=
			xc::get_standard_xcursor  xsession  xc::cursors_old::sb_left_arrow;

		    fun do_menu (menu_but, point, time, m_mailop)
			=
			{	xc::change_active_grab_cursor  xsession  menu_cursor;

			    xlogger::make_thread  "Menu: track_mouse"  track_mouse;

			    take_from_mailslot' reply_slot;
			}
			where

			    window_topleft
				=
				xc::window_point_to_screen_point  window  xg::point::zero;


			    reply_slot
				=
				make_mailslot ();


			    (create_menu' (xg::point::add (window_topleft, point), fore_pen, font, menu_items))
				->
				(menu_origin, menu_win, menu_dict, select_item);
				


			    m_mailop
				=
				m_mailop
				    ==>
				    (translate_mouse (xg::point::subtract (window_topleft, menu_origin)));


			    fun send_selection NULL
				    =>
				    put_in_mailslot (reply_slot, NULL);

				send_selection (THE { item, text_pos, box, text } )
				    =>
				    put_in_mailslot (reply_slot, THE text);
			    end;


			    drawable
				=
				xc::drawable_of_window  menu_win;


			    fun flip_on { item, text_pos, box, text }
				=
				{   xc::fill_box    drawable  fore_pen  box;
				    xc::draw_transparent_string  drawable  back_pen  font (text_pos, text);
				};


			    fun flip_off { item, text_pos, box, text }
				=
				{   xc::fill_box    drawable  back_pen  box;
				    xc::draw_transparent_string  drawable  fore_pen  font;

				    (text_pos, text);
				};


			    fun mouse_loop (cur_item, point)
				=
				{   cur_item
					=
					case (cur_item, select_item point)
					    #
					    (THE a, THE b)
						=>
						if (a.item == b.item)
						    #
						    cur_item;
						else
						    flip_off a;
						    flip_on  b;
						    THE b;
						fi;

					    (THE a, NULL)
						=>
						{   flip_off a;
						    NULL;
						};

					    (NULL, THE b)
						=>
						{   flip_on b;
						    THE b;
						};

					    (NULL, NULL)
						=>
						NULL;
					esac;


				    case (block_until_mailop_fires  m_mailop)
					#
					(xc::MOUSE_MOTION { screen_point, window_point, ... } )
					    =>
					    mouse_loop (cur_item, window_point);

					(xc::MOUSE_LAST_UP { mouse_button, ... } )
					    =>
					    (cur_item, TRUE);

					(xc::MOUSE_UP { mouse_button, state, ... } )
					    =>
					    if (mouse_button == menu_but)   (cur_item, FALSE);
					    else                            mouse_loop (cur_item, point);
					    fi;

					_   =>
					    mouse_loop (cur_item, point);
				    esac;
				};


			    fun all_up ()
				=
				case (block_until_mailop_fires  m_mailop)
				    #
				    xc::MOUSE_LAST_UP _ =>  ();
				    _                   =>  all_up();
				esac;


			    fun track_mouse ()
				=
				{   my (selection, last_up)
					=
					mouse_loop (NULL, xg::point::add (point, xg::point::subtract (window_topleft, menu_origin)));

				    xc::destroy_window  menu_win;

				    if (not last_up)   all_up ();   fi;

				    send_selection selection;
				};

			end;		# fun do_menu
		  end;			# fun popup_menu window
	end;				# stipulate
    };
end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2014,
## released per terms of SMLNJ-COPYRIGHT.
