## textbuffer-imp.pkg
#
# This package contains (typically) the contents
# of one file open for editing.  It is not a
# GUI widget;  display of the textbuffer contents
# is handled by
#
#     src/lib/x-kit/widget/edit/textpane.pkg
#
# In "Model/View/Controller" terms, textbuffer-imp.pkg
# is the Model and textpane.pkg is the View+Controller.
#
# See also:
#     src/lib/x-kit/widget/edit/textpane.pkg
#     src/lib/x-kit/widget/edit/textedit.pkg
#     src/lib/x-kit/widget/edit/editboss-imp.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;				# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  client_to_atom;				# client_to_atom		is from   src/lib/x-kit/xclient/src/iccc/client-to-atom.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package r2k =  xevent_router_to_keymap;			# xevent_router_to_keymap	is from   src/lib/x-kit/xclient/src/window/xevent-router-to-keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package sep =  client_to_selection;				# client_to_selection		is from   src/lib/x-kit/xclient/src/window/client-to-selection.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package x2s =  xclient_to_sequencer;			# xclient_to_sequencer		is from   src/lib/x-kit/xclient/src/wire/xclient-to-sequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/old/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  client_to_window_watcher;			# client_to_window_watcher	is from   src/lib/x-kit/xclient/src/window/client-to-window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg
    #

    #
    package evt =  gui_event_types;				# gui_event_types		is from   src/lib/x-kit/widget/gui/gui-event-types.pkg
    package gts =  gui_event_to_string;				# gui_event_to_string		is from   src/lib/x-kit/widget/gui/gui-event-to-string.pkg
    package gt  =  guiboss_types;				# guiboss_types			is from   src/lib/x-kit/widget/gui/guiboss-types.pkg

    package a2r =  windowsystem_to_xevent_router;		# windowsystem_to_xevent_router	is from   src/lib/x-kit/xclient/src/window/windowsystem-to-xevent-router.pkg

    package gd  =  gui_displaylist;				# gui_displaylist		is from   src/lib/x-kit/widget/theme/gui-displaylist.pkg

    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package err =  error_message;				# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg

    package bt  =  gui_to_sprite_theme;				# gui_to_sprite_theme		is from   src/lib/x-kit/widget/theme/sprite/gui-to-sprite-theme.pkg
    package ct  =  gui_to_object_theme;				# gui_to_object_theme		is from   src/lib/x-kit/widget/theme/object/gui-to-object-theme.pkg
    package wt  =  widget_theme;				# widget_theme			is from   src/lib/x-kit/widget/theme/widget/widget-theme.pkg

#   package os  =  guiboss_to_spritespace;			# guiboss_to_spritespace	is from   src/lib/x-kit/widget/space/sprite/guiboss-to-spritespace.pkg
#   package cs  =  guiboss_to_objectspace;			# guiboss_to_objectspace	is from   src/lib/x-kit/widget/space/object/guiboss-to-objectspace.pkg


    package boi =  spritespace_imp;				# spritespace_imp		is from   src/lib/x-kit/widget/space/sprite/spritespace-imp.pkg
    package cai =  objectspace_imp;				# objectspace_imp		is from   src/lib/x-kit/widget/space/object/objectspace-imp.pkg
    package pai =  widgetspace_imp;				# widgetspace_imp		is from   src/lib/x-kit/widget/space/widget/widgetspace-imp.pkg

    #    
    package gtg =  guiboss_to_guishim;				# guiboss_to_guishim		is from   src/lib/x-kit/widget/theme/guiboss-to-guishim.pkg

    package b2s =  spritespace_to_sprite;			# spritespace_to_sprite		is from   src/lib/x-kit/widget/space/sprite/spritespace-to-sprite.pkg
    package c2o =  objectspace_to_object;			# objectspace_to_object		is from   src/lib/x-kit/widget/space/object/objectspace-to-object.pkg

    package s2b =  sprite_to_spritespace;			# sprite_to_spritespace		is from   src/lib/x-kit/widget/space/sprite/sprite-to-spritespace.pkg
    package o2c =  object_to_objectspace;			# object_to_objectspace		is from   src/lib/x-kit/widget/space/object/object-to-objectspace.pkg

#   package c2g =  gadget_to_editboss;				# gadget_to_editboss		is from   src/lib/x-kit/widget/gui/client-to-guiboss.pkg
    package g2p =  gadget_to_pixmap;				# gadget_to_pixmap		is from   src/lib/x-kit/widget/theme/gadget-to-pixmap.pkg

    package iuw =  issue_unique_widget_id;			# issue_unique_widget_id	is from   src/lib/x-kit/widget/gui/issue-unique-widget-id.pkg

    package im  =  int_red_black_map;				# int_red_black_map		is from   src/lib/src/int-red-black-map.pkg
#   package is  =  int_red_black_set;				# int_red_black_set		is from   src/lib/src/int-red-black-set.pkg

    package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
    package r64 =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
    package g2d =  geometry2d;					# geometry2d			is from   src/lib/std/2d/geometry2d.pkg
    package g2j =  geometry2d_junk;				# geometry2d_junk		is from   src/lib/std/2d/geometry2d-junk.pkg

    package ebt =  editboss_types;				# editboss_types		is from   src/lib/x-kit/widget/edit/editboss-types.pkg

    package e2g =  editboss_to_guiboss;				# editboss_to_guiboss		is from   src/lib/x-kit/widget/edit/editboss-to-guiboss.pkg

    package p2t =  textpane_to_textbuffer;			# textpane_to_textbuffer	is from   src/lib/x-kit/widget/edit/textpane-to-textbuffer.pkg
    package g2b =  gadget_to_editboss;				# gadget_to_editboss		is from   src/lib/x-kit/widget/edit/gadget-to-editboss.pkg

    package que =  queue;					# queue				is from   src/lib/src/queue.pkg
    package nl  =  red_black_numbered_list;			# red_black_numbered_list	is from   src/lib/src/red-black-numbered-list.pkg
    package tbj =  textbuffer_junk;				# textbuffer_junk		is from   src/lib/x-kit/widget/edit/textbuffer-junk.pkg
    package tbt =  textbuffer_types;				# textbuffer_types		is from   src/lib/x-kit/widget/edit/textbuffer-types.pkg
    package mj  =  mode_junk;					# mode_junk			is from   src/lib/x-kit/widget/edit/mode-junk.pkg


    tracefile   =  "widget-unit-test.trace.log";

    nb = log::note_on_stderr;					# log				is from   src/lib/std/src/log.pkg

Dummy3 = p2t::Textpane_To_Textbuffer;	# XXX SUCKO DELETEME. This is a quick hack to make sure the package compiles during early development of it.
Dummy4 = tbj::Foo;			# XXX SUCKO DELETEME. This is a quick hack to make sure the package compiles during early development of it.
Dummy5 = tbt::Bar;			# XXX SUCKO DELETEME. This is a quick hack to make sure the package compiles during early development of it.
herein

    package textbuffer_imp
    :       Textbuffer_Imp															# Textbuffer_Imp		is from   src/lib/x-kit/widget/edit/textbuffer-imp.api
    {
	Textbuffer_State															# Holds all nonephemeral mutable state maintained by package.
	  =
	  { state:		Ref(p2t::Textstate),												# Current visible state of textbuffer.
	    history:		Ref(que::Queue(p2t::Undostate)),										# Recent visible states of textbuffer, to support undo functionality.
	    history_len:	Ref(Int),													# Max length of 'history' list.
	    history_max:	Ref(Int),													# Max length of 'history' list.
	    filename:		Ref(String),													# Name of file being edited in buffer, or NULL if no file is associated with buffer contents.
	    buffername:		Ref(String),													# Name of buffer for display purposes, typically defaults to filename.
	    dirty:		Ref(Bool)													# TRUE if buffer contents have been changed since being read from disk.
	  };


	Imports = {																# Ports we use, provided by other imps.
		  };


	Me_Slot = Mailslot( { imports:		Imports,
			      me:		Textbuffer_State,
			      textbuffer_arg:	p2t::Textbuffer_Arg,
			      statewatchers:	Ref(im::Map( (Id, p2t::Textbuffer_Statechange -> Void) )),
			      run_gun':		Run_Gun,
			      end_gun':		End_Gun
			    }
			  );

	Exports	= {																# Ports we provide for use by other imps.
		    textpane_to_textbuffer:	p2t::Textpane_To_Textbuffer
		  };


	Textbuffer_Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Runstate =    {																# These values will be statically globally visible throughout the code body for the imp.
			id:			Id,
			me:			Textbuffer_State,										# State which is preserved across imp shutdown/restart cycles.
			textbuffer_arg:		p2t::Textbuffer_Arg,
			imports:		Imports,											# Imps to which we send requests.
			to:			Replyqueue,											# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
			statewatchers:		Ref(im::Map( (Id, p2t::Textbuffer_Statechange -> Void ))),
			end_gun':		End_Gun												# We shut down the microthread when this fires.
		      };

	Textbuffer_Q    = Mailqueue( Runstate -> Void );


	fun run ( textbuffer_q:			Textbuffer_Q,											# 
		  #
		  runstate as
		  {																# These values will be statically globally visible throughout the code body for the imp.
		    me:				Textbuffer_State,										# State which is preserved across imp shutdown/restart cycles.
		    textbuffer_arg:		p2t::Textbuffer_Arg,
		    imports:			Imports,											# Imps to which we send requests.
		    to:				Replyqueue,											# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    statewatchers:		Ref(im::Map( (Id, p2t::Textbuffer_Statechange -> Void ))),
		    end_gun':			End_Gun,											# 
		    id:				Id
		  }
		)
	    =
	    {	loop ();
	    }
	    where


		#
		fun loop ()															# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    end_gun'                          ==>  shut_down_textbuffer_imp',
			    take_from_mailqueue' textbuffer_q ==>  do_textbuffer_plea
			];

			loop ();
		    }	
		    where
			fun do_textbuffer_plea  thunk
			    =
			    thunk runstate;
			#
			fun shut_down_textbuffer_imp' ()
			    =
			    {
				thread_exit { success => TRUE };										# Will not return.	
			    };
		    end;
	    end;	



	#
	fun startup   (id: Id,   reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports) ))   ()							# Root fn of imp microthread.  Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		textpane_to_textbuffer
		  =
		  { id,
		    #
		    get_maxline,
		    pass_maxline,

		    get_line,
		    pass_line,

		    get_lines,
		    pass_lines,

		    get_textstate,
		    pass_textstate,

		    pass_edit_result,

		    undo,

		    get_dirty,
		    pass_dirty,

		    get_filename,
		    set_filename,
		    pass_filename,

		    get_buffername,
		    set_buffername,
		    pass_buffername,

		    note_statewatcher,
		    drop_statewatcher,

		    reload_from_file,
		    save_to_file
		  };

		exports	    = { textpane_to_textbuffer
			      };
				
		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports));										# Return value from textbuffer_egg'().

		(take_from_mailslot  me_slot)													# Imports from textbuffer_egg'().
		    ->
		    { me, textbuffer_arg, imports, statewatchers, run_gun', end_gun' };

		block_until_mailop_fires  run_gun';												# Wait for the starting gun.

		runstate =    { id,
				me,
				textbuffer_arg,
				imports,
				to,
				statewatchers,
				end_gun'
			      };

		tell_statewatchers_full_state  runstate;											# Make sure statewatchers start out with full update.

		run (textbuffer_q, runstate);		 											# Will not return.
	    }
	    where
		textbuffer_q     =  make_mailqueue (get_current_microthread()):  Textbuffer_Q;


		fun tell_statewatchers
		      (
			statewatchers:		im::Map( (Id, p2t::Textbuffer_Statechange -> Void) ),
			statechange:		p2t::Textbuffer_Statechange
		      )
		    =
		    im::keyed_apply   tell_watcher   statewatchers
		    where
			fun tell_watcher
			      (
				key:			Int,											# == (id_to_int id)
				( id:			Id,											# Unique id identifying this watcher.
				  statewatcher:		p2t::Textbuffer_Statechange -> Void
				)
			      )
			    =
			    statewatcher  statechange;
		    end;

		fun tell_statewatcher_full_state
		      (
			statewatcher:		p2t::Textbuffer_Statechange -> Void,
			r:			Runstate
		      )	
		    =
		    {   statewatcher  (p2t::TEXTSTATE  (r.id, { was => *r.me.state,      now => *r.me.state      }));
			statewatcher  (p2t::FILENAME   (r.id, { was => *r.me.filename,   now => *r.me.filename   }));
			statewatcher  (p2t::BUFFERNAME (r.id, { was => *r.me.buffername, now => *r.me.buffername }));
		    };

		fun tell_statewatchers_full_state
		      (
			r:			Runstate
		      )
		    =
		    im::keyed_apply   tell_watcher   *r.statewatchers
		    where
			fun tell_watcher
			      (
				key:			Int,											# == (id_to_int id)
				( id:			Id,											# Unique id identifying this watcher.
				  statewatcher:		p2t::Textbuffer_Statechange -> Void
				)
			      )
			    =
			    tell_statewatcher_full_state (statewatcher, r);
		    end;

		fun line_range (textlines: mj::Textlines,  firstline: Int,  lastline: Int):  List(String)
		    =
		    lines (lastline, [])
		    where
			fun lines (i: Int, result: List(String))
			    =
			    if (i < firstline)    result;
			    else
				result =    case (nl::find (textlines, i))
						#
						THE line => line ! result;
						NULL	 =>        result;
					    esac;

				lines  (i - 1,  result);
			    fi;
		    end;

# 		fun editfn_out__to__editfn_out_internal
# 		       (result: mj::Editfn_Out)
# 		    =


		fun changed_textlines ([]:  List(mj::Editfn_Out_Option)) =>  NULL;
		    changed_textlines (mj::TEXTLINES textlines ! _)	 =>  THE textlines;
		    changed_textlines (_ ! rest)			 =>  changed_textlines rest;
		end;


		fun do_pass_edit_result
		      (
			{ id, me, statewatchers, ... }:	Runstate,
			#
			arg as
			{ point_and_mark:	p2t::Point_And_Mark,						# 'point' is the visible cursor. 'mark' (if set) is the other end of the selected region. (Emacs nomenclature.)
			  lastmark:		Null_Or( g2d::Point ),
			  editfn:		mj::Editfn_In -> mj::Editfn_Out,
			  #
			  widget_to_guiboss:	gt::Widget_To_Guiboss,						# Mainly for access to get_cutbuffer_contents/set_cutbuffer_contents in gadget_to_editboss.  NB: Passing gadget_to_editboss here instead results in a package cycle.
			  log_undo_info:	Bool,
			  keystring:		String								# User keystroke that invoked this editfn.
			}
		      )
		    =
		    {   was = *me.state;
			#
			gadget_to_editboss
			    =
			    g2b::find__gadget_to_editboss
			      (
				widget_to_guiboss.g,
				"textbuffer_imp::do_pass_edit_result"
			      );

			editfn_in =   { args			=>  [],
					textlines		=>  was.textlines,
					point  			=>  point_and_mark.point,
					mark			=>  point_and_mark.mark,
					lastmark,
					get_cutbuffer_contents	=>  gadget_to_editboss.get_cutbuffer_contents,
					set_cutbuffer_contents	=>  gadget_to_editboss.set_cutbuffer_contents,
					keystring
				      };	

			editfn_out =	(editfn editfn_in)
					except _ = NULL;												# Handle any uncaught exceptions in editfn. (Shouldn't happen.)


			case editfn_out
			    #
			    NULL => editfn_out;														# Editfn aborted, no changes to process.

			    THE editfn_out_options													# Editfn did not abort ...
				=>
				case (changed_textlines editfn_out_options)
				    #
				    NULL => editfn_out;													# Editfn did NOT change contents of textbuffer, so we're done.

				    THE new_textlines													# Editfn DID change contents of textbuffer.
					=>
					{   now     = { textlines =>  new_textlines,
							editcount =>  was.editcount + 1
						      };

					    me.state   :=  now;
					    me.dirty   :=  TRUE;

					    if log_undo_info
						#
						me.history :=  que::push (*me.history, was);								# Add previous state to history.

						if(*me.history_len <  *me.history_max)									# Maybe we should have a bounded-queue.pkg that handles all this stuff internally?
						    me.history_len := *me.history_len + 1;								# History is under max allowed, so let it get one longer.
						else
						    (que::pull *me.history) -> (newhistory, _);								# History is at max allowed, pull an entry to keep it under limit.
						    #
						    me.history     := newhistory;
						fi;
					    fi;

					    tell_statewatchers  (*statewatchers,  p2t::TEXTSTATE  (id, { was, now }));

					    editfn_out;													# Return all updates to caller.
					};
				esac;
			esac;
		    };

		fun do_undo ({ id, me, statewatchers, ... }:	Runstate)
		    =
		    if (*me.history_len > 0)
			#
			(*me.state)             -> (             was);
			(que::pull *me.history) -> (new_history, now);

			now 	= the now;												# Should not be NULL because we checked for history_len==0 above.

			me.history	:=  new_history;
			me.history_len  := *me.history_len - 1;
			me.state	:=  now;

			tell_statewatchers  (*statewatchers,  p2t::UNDO (id, { was, now }));
		    fi;



		#################################################################################
		# editboss interface fns::
		#
		#

		#################################################################################
		# texteditor interface fns::
		#
		#

		fun get_maxline ()														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, the (nl::max_key state.textlines));
				}
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #
		fun pass_maxline  (replyqueue: Replyqueue)  (reply_handler: Int -> Void)							# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, the (nl::max_key state.textlines));
				}
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun get_line (i: Int)														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Null_Or(String) );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, (nl::find (state.textlines, i)));
				}
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #		
		fun pass_line  (replyqueue: Replyqueue)  (i: Int)  (reply_handler: Null_Or(String) -> Void)					# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Null_Or(String) );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, (nl::find (state.textlines, i)));
				}
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun get_lines (first: Int,  last: Int)												# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( List(String) );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, (line_range (state.textlines, first, last)));
				}
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #		
		fun pass_lines  (replyqueue: Replyqueue)  (first: Int, last: Int)  (reply_handler: List(String) -> Void)			# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( List(String) );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				{   state = *me.state;
				    #
				    put_in_oneshot (reply_oneshot, (line_range (state.textlines, first, last)));
				}
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun get_textstate ()														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( p2t::Textstate );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.state)
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #
		fun pass_textstate  (replyqueue: Replyqueue)  (reply_handler: p2t::Textstate -> Void)						# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( p2t::Textstate );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.state)
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun pass_edit_result														# PUBLIC.
		      #		
		      (arg:		p2t::Edit_Arg)
		      #		
		      (replyqueue:	Replyqueue)
		      #		
		      (reply_handler:	mj::Editfn_Out -> Void)
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( mj::Editfn_Out );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ (r: Runstate)
				=
				put_in_oneshot (reply_oneshot, do_pass_edit_result (r, arg))
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun undo ()															# PUBLIC.
		    =
		    {   put_in_mailqueue  (textbuffer_q,
			    #
			    \\ (r: Runstate)
				=
				do_undo r
			);
		    };


		fun get_dirty ()														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Bool );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.dirty)
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #
		fun pass_dirty  (replyqueue: Replyqueue)  (reply_handler: Bool -> Void)								# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Bool );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.dirty)
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun set_filename (filename: String)												# PUBLIC.
		    =
		    {	put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				{   tell_statewatchers  (*statewatchers, p2t::FILENAME (id, { now => filename, was => *me.filename }));
				    #
				    me.filename := filename;
				}
			);

		    };
		    #
		fun get_filename ()														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( String );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.filename)
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #
		fun pass_filename  (replyqueue: Replyqueue)  (reply_handler: String -> Void)							# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( String );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.filename)
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun set_buffername (buffername: String)												# PUBLIC.
		    =
		    {	put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				{   tell_statewatchers  (*statewatchers,  p2t::BUFFERNAME (id, { now => buffername, was => *me.buffername }));
				    #
				    me.buffername := buffername;
				}
			);
		    };
		    #
		fun get_buffername ()														# PUBLIC.
		    =
		    {	reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( String );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.buffername)
			);

			get_from_oneshot  reply_oneshot;
		    };
		    #
		fun pass_buffername  (replyqueue: Replyqueue)  (reply_handler: String -> Void)							# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( String );
			#
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, *me.buffername)
			);
 
			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun note_statewatcher														# PUBLIC.
		      (
			statewatcher_id:	Id,
			statewatcher:		p2t::Textbuffer_Statechange -> Void
		      )	
		    =
		    {   put_in_mailqueue  (textbuffer_q,
			    #
			    \\ (runstate as { id, me, statewatchers, ... }: Runstate)
				=
				{   statewatchers :=  im::set( *statewatchers,
								id_to_int  statewatcher_id,
								(statewatcher_id, statewatcher)
							      );
				    tell_statewatcher_full_state (statewatcher, runstate);							# Make sure new statewatcher starts out with full update.
				}
			);
		    };
		    #
		fun drop_statewatcher  (statewatcher_id:  Id)											# PUBLIC.
		    =
		    {   
			put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				statewatchers :=  im::drop (*statewatchers,  id_to_int statewatcher_id)
			);
		    };


		fun reload_from_file  ()													# PUBLIC.
		    =
		    {   put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				{
# XXX BUGGO FIXME: Not written yet.
				}
			);
		    };
		    #
		fun save_to_file  ()														# PUBLIC.
		    =
		    {   put_in_mailqueue  (textbuffer_q,
			    #
			    \\ ({ id, me, statewatchers, ... }: Runstate)
				=
				{
# XXX BUGGO FIXME: Not written yet.
				}
			);
		    };
	    end;

	#
	fun process_options
	      (
		options: List(p2t::Textbuffer_Option),
		#
		{ name,
		  id,
		  statewatchers,
		  filename,
		  text
		}
	      )
	    =
	    {   my_name			=  REF name;
		my_id			=  REF id;
		my_statewatchers	=      statewatchers;											# Already has a REF on it.
		my_filename		=  REF filename;	
		my_text			=  REF text;
	#
		apply  do_option  options
		where
		    fun do_option (p2t::MICROTHREAD_NAME	n)  =>  my_name		 :=  n;
			do_option (p2t::ID			i)  =>  my_id		 :=  i;
			do_option (p2t::STATEWATCHER       (id, w)) =>	my_statewatchers :=  im::set (*my_statewatchers, id_to_int id, (id, w));
			do_option (p2t::INITIAL_FILENAME	n)  =>	my_filename      :=  n;
			do_option (p2t::ASCII			n)  =>	my_text		 :=  n;
		    end;
		end;

		{ name		 =>  *my_name,
		  id 		 =>  *my_id,
		  statewatchers  =>   my_statewatchers,												# Leave the REF on it.
		  filename       =>  *my_filename,
		  text	         =>  *my_text
		};
	    };


        ##########################################################################################
	# PUBLIC.
	#
	fun make_textbuffer_egg
	      (textbuffer_arg:		p2t::Textbuffer_Arg)											# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	    =
	    {	textbuffer_arg ->  (buffername, textbuffer_options);
		#
		(process_options
		  ( textbuffer_options,
		    { name		=>  buffername,
		      id		=>  id_zero,
		      statewatchers	=>  REF im::empty,
		      filename		=>  "",
		      text		=>  ""
		    }
		) )
		    ->
		    { name,
		      id,
		      statewatchers,
		      filename,
		      text
		    };
	
		my (id, textbuffer_options)
		    =
		    if (id_to_int(id) == 0)
			id = issue_unique_id();													# Allocate unique imp id.
			(id, p2t::ID id ! textbuffer_options);											# Make our id stable across stop/restart cycles.
		    else
			(id, textbuffer_options);
		    fi;

		me =	{ state       =>  REF textstate,
			  history     =>  REF que::empty_queue,
			  history_len =>  REF   0,
			  history_max =>  REF 100,
			  filename    =>  REF filename,
# XXX BUGGO FIXME We need to ensure here that buffername is unique.  (Or is that an upstream responsibility? Anyhow, someone needs to be doing that.)
			  buffername  =>  REF buffername,
			  dirty	      =>  REF FALSE	
			}
			where
			    textstate =   { textlines =>  nl::from_list (string::lines text),
					    editcount =>  0
					  };
			end;

		\\ () = {   reply_oneshot = make_oneshot_maildrop():  Oneshot_Maildrop( (Me_Slot, Exports) );					# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
			    #
			    xlogger::make_thread  name  (startup  (id, reply_oneshot));								# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports);

			    fun phase3														# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{
				    put_in_mailslot  (me_slot, { me, textbuffer_arg, imports, statewatchers, run_gun', end_gun' });
				};

			    (exports, phase3);
			};
	    };
    };

end;





##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2014,
## released per terms of SMLNJ-COPYRIGHT.
