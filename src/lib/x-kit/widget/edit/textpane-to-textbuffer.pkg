## textpane-to-textbuffer.pkg
#
# Here we define the port which
#
#     src/lib/x-kit/widget/edit/textbuffer-imp.pkg
#
# exports to
#
#     src/lib/x-kit/widget/edit/textpane.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib



stipulate
    include package   threadkit;								# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ebt =  editboss_types;								# editboss_types		is from   src/lib/x-kit/widget/edit/editboss-types.pkg

    package g2d =  geometry2d;									# geometry2d			is from   src/lib/std/2d/geometry2d.pkg

    package nl  =  red_black_numbered_list;							# red_black_numbered_list	is from   src/lib/src/red-black-numbered-list.pkg
    package iuw =  issue_unique_widget_id;							# issue_unique_widget_id	is from   src/lib/x-kit/widget/gui/issue-unique-widget-id.pkg

    package e2m =  textedit_to_minibuffer;							# textedit_to_minibuffer	is from   src/lib/x-kit/widget/edit/textedit-to-minibuffer.pkg
    package e2p =  textedit_to_textpane;							# textedit_to_textpane		is from   src/lib/x-kit/widget/edit/textedit-to-textpane.pkg
    package l2p =  lineeditor_to_textpane;							# lineeditor_to_textpane	is from   src/lib/x-kit/widget/edit/lineeditor-to-textpane.pkg
    package m2e =  minibuffer_to_textedit;							# minibuffer_to_textedit	is from   src/lib/x-kit/widget/edit/minibuffer-to-textedit.pkg
    package p2e =  textpane_to_textedit;							# textpane_to_textedit		is from   src/lib/x-kit/widget/edit/textpane-to-textedit.pkg
    package p2l =  textpane_to_lineeditor;							# textpane_to_lineeditor	is from   src/lib/x-kit/widget/edit/textpane-to-lineeditor.pkg
    package p2s =  textpane_to_statusline;							# textpane_to_statusline	is from   src/lib/x-kit/widget/edit/textpane-to-statusline.pkg
    package s2p =  statusline_to_textpane;							# statusline_to_textpane	is from   src/lib/x-kit/widget/edit/statusline-to-textpane.pkg

    package b2m =  editboss_to_minibuffer;							# editboss_to_minibuffer	is from   src/lib/x-kit/widget/edit/editboss-to-minibuffer.pkg
    package b2s =  editboss_to_statusline;							# editboss_to_statusline	is from   src/lib/x-kit/widget/edit/editboss-to-statusline.pkg
    package b2l =  editboss_to_lineeditor;							# editboss_to_lineeditor	is from   src/lib/x-kit/widget/edit/editboss-to-lineeditor.pkg
    package b2e =  editboss_to_textedit;							# editboss_to_textedit		is from   src/lib/x-kit/widget/edit/editboss-to-textedit.pkg
    package b2p =  editboss_to_textpane;							# editboss_to_textpane		is from   src/lib/x-kit/widget/edit/editboss-to-textpane.pkg
herein

    # This port is implemented in:
    #
    #     src/lib/x-kit/widget/edit/textbuffer-imp.pkg
    #
    package textpane_to_textbuffer {
	#
	Textlines = nl::Numbered_List(String);							# We store our file contents in this, line by line.

	Textstate
	  =
	  { textlines:		Textlines,							# Complete text contents of textbuffer.
	    editcount:		Int								# Count of edits applied.  Intended to allow clients to quickly detect whether any changes have been made since they last polled us.
	  };

	Undostate = Textstate;									# Synonym for readability.

	Cursor_And_Mark
	  =
	  { cursor:		g2d::Point,							# Note that cursor and mark are per-textpane, not per-textbuffer. (This matters because there may be multiple textpanes open on one textbuffer).
	    mark:		Null_Or(g2d::Point)						# NULL means the emacs-style 'mark' is not currently set.
	  };

	Editstate
	  =
	  { textlines:		Textlines,
	    cursor:		g2d::Point,							# As in Cursor_And Mark.
	    mark:		Null_Or(g2d::Point)						# 
	  };

	Textbuffer_Statechange									# Used to tell clients (mostly textpane.pkg) about changes in our state.
	  #											# The 'Id' field in each case is t2t::Textpane_To_Textbuffer.id;  it may be used by client to identify and discard stale statechanges which arrive after calling drop_statewatcher().
	  = TEXTSTATE		(Id, { was: Textstate, now: Textstate })
	  | UNDO		(Id, { was: Textstate, now: Undostate })
	  | FILENAME		(Id, { was: String,    now: String    })
	  | BUFFERNAME		(Id, { was: String,    now: String    })
	  ;

	Edit_Arg										# Arg type for pass_edit_result().
	  =
	  { cursor_and_mark:	Cursor_And_Mark,
	    change_fn:		Editstate -> Editstate,
	    #
	    log_undo_info:	Bool								# If unfo_info is FALSE no entry will be made in the undo history.
	  };

	Textpane_To_Textbuffer
	  =
	  { id:				Id,							# Unique id to facilitate storing textpane_to_textbuffer instances in indexed datastructures like red-black trees.
	    #
	    get_maxline:		Void -> Int,						# Max currently valid line number.
	    pass_maxline:		Replyqueue -> (Int -> Void) -> Void,
	    #
	    get_line:			Int -> Null_Or(String),
	    pass_line:			Replyqueue
					 -> Int
					 -> (Null_Or(String) -> Void)
					 -> Void,
	    #
	    get_lines:			(Int, Int) -> List(String),
	    pass_lines:			Replyqueue
					 -> (Int, Int)
					 -> (List(String) -> Void)
					 -> Void,
	    #
	    get_textstate:		Void -> Textstate,					# 
	    pass_textstate:		Replyqueue -> (Textstate -> Void) -> Void,
	    #
	    pass_edit_result:		Edit_Arg
					 -> Replyqueue
					 -> (Cursor_And_Mark -> Void)
					 -> Void,						# 

	    undo:			Void -> Void,

	    get_dirty:			Void -> Bool,						# TRUE iff textbuffer contents are currently out of sync with diskfile contents.
	    pass_dirty:			Replyqueue -> (Bool -> Void) -> Void,

	    set_filename:		String -> Void,						# Filename controls what file textuffer contents are read/written from/to.
	    get_filename:		Void -> String,						# 
	    pass_filename:		Replyqueue -> (String -> Void) -> Void,

	    set_buffername:		String -> Void,						# Buffername uniquely identifies textbuffer to interactive user.
	    get_buffername:		Void -> String,						# 
	    pass_buffername:		Replyqueue -> (String -> Void) -> Void,
												# The statewatcher protocol allows others imps (including other texteditors and also arbitrary application imps) to monitor changes to a given textbuffer.
	    note_statewatcher:		(Id, (Textbuffer_Statechange -> Void)) -> Void,		# 'Id' may be the id of the calling imp or may be freshly allocated via issue_unique_id. It identifies this particular watcher/connection for drop_statewatcher.
	    drop_statewatcher:		Id -> Void,						# 'Id' must match that given to note_statewatcher (or texteditor_imp::STATEWATCHER).

	    reload_from_file:		Void -> Void,
	    save_to_file:		Void -> Void
	  };



	Textbuffer_Option
	  #
	  =  MICROTHREAD_NAME	String								# 
	  |  ID			Id								# Stable, unique id for imp.
	  |  STATEWATCHER	(Id, (Textbuffer_Statechange -> Void))				# Equivalent to t2t::Textpane_To_Textbuffer.note_statewatcher(). 'Id' may be the id of the calling imp or may be freshly allocated via issue_unique_id. It identifies this particular watcher/connection for drop_statewatcher.
	  |  INITIAL_FILENAME	String								# 
	  |  ASCII		String
	  ;	

	Textbuffer_Arg =  (String, List(Textbuffer_Option));					# Required argument is buffername.
    };
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2015,
## released per terms of SMLNJ-COPYRIGHT.
