## time-limit.pkg -- run a computation under a time limit.

# Compiled by:
#     src/lib/src/lib7.make6



###                   "You will never find time for anything.
###                    If you want time you must make it."
###
###                                    -- Charles Buxton



package time_limit: (weak)
api {
    exception TIME_OUT;
    time_limit:  time::Time -> (X -> Y) -> X -> Y;
}
{
    exception TIME_OUT;

    fun time_limit t f x
        =
        {   setitimer =   interval_timer::set_interval_timer;

	    fun timer_on  () =  ignore (setitimer (THE t));
	    fun timer_off () =  ignore (setitimer  NULL  );

	    escape_fate
                =
                fate::call_with_current_fate
                    (fn fate =
                          {   fate::call_with_current_fate
                                  (fn fate' =  (fate::resume_fate fate fate'));

		              timer_off ();

		              raise exception TIME_OUT;
                          }
                    );

	    fun handler _
                =
                escape_fate;

	    signals::set_signal_handler
                (signals::alarm_signal, signals::HANDLER handler);

	    timer_on ();

	    ( (f x)
              except
                  ex = { timer_off ();
                         raise exception ex;
                       }
            )
	    before
                timer_off ();
	};

};	#  package time_limit 


## COPYRIGHT (c) 1993 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
