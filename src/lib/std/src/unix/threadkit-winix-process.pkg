## threadkit-winix-process.pkg

# Compiled by:
#     src/lib/std/stdlib.make6



# The generic process control interface.

stipulate
    package s  = thread_scheduler;						# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    package pm = process_manager;						# process_manager		is from   src/lib/src/lib/thread-kit/src/unix-process-manager.pkg

    package pp = posix;								# posix				is from   src/lib/std/src/posix/posix.pkg
    package p  = winix::process;						# winix				is from   src/lib/std/winix.pkg
										# winix::process		is from   src/lib/std/src/unix/winix-process.pkg
herein

    package threadkit_winix_process: (weak)  Threadkit_Winix_Process {		# Threadkit_Winix_Process	is from   src/lib/src/lib/thread-kit/src/winix/threadkit-winix-process.api


										# fate				is from   src/lib/std/src/nj/fate.pkg

	Status = p::Status;

	success = p::success;
	failure = p::failure;

	# * NOTE: we probably need to disable timer signals here *  XXX BUGGO FIXME

	fun system' cmd
	    =
	    {   s::stop_timer();

		case (pp::fork())
		    #
		    NULL    =>  pp::exec ("/bin/sh", ["sh", "-c", cmd]) pp::exit 0u127;
		    #
		    THE pid =>  {   s::restart_timer();
				    pid;
				};
		esac;
	    };

	fun system_mailop cmd
	    =
	    {   pid =  system' cmd;

		mailop
		    =
		    {   s::disable_thread_switching ();

			pm::add_pid pid
			before
			    s::reenable_thread_switching ();
		    };

		mailop::wrap
		  ( mailop,
		    fn pp::W_EXITED => p::success;
		       _            => p::failure;
		    end
		  );
	    };

	system
	    =
	    mailop::do_mailop
	    o
	    system_mailop;

	fun at_exit _
	    =
	    raise exception FAIL "winix::process::at_exit unimplemented";		# XXX BUGGO FIXME

	fun exit status      =  { s::disable_thread_switching();   fate::resume_fate  *s::shutdown_hook  (TRUE,  status); };
	fun terminate status =  { s::disable_thread_switching();   fate::resume_fate  *s::shutdown_hook  (FALSE, status); };

	get_env = p::get_env;
    };
end;

## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
