## net-service-db.pkg

# Compiled by:
#     src/lib/std/src/stdlib-core.make6



###		"I am not young enough to know everything."
###                                       -- Oscar Wilde

  

package net_service_db: (weak)  Net_Service_Db	{	# Net_Service_Db	is from   src/lib/std/src/socket/net-service-db.api

    fun netdb_fun x = c_interface::c_function "Lib7-Sockets" x;

     Entry = SERVENT  {
	  name:  String,
	  aliases:  List( String ),
	  port:  Int,
	  protocol:  String
	};

    stipulate
        fun conc field' (SERVENT a) = field' a;
    herein
	name = conc .name;
	aliases = conc .aliases;
	port = conc .port;
	protocol = conc .protocol;
    end; #  local 

  #  Server DB query functions 
    stipulate
       Servent = ((String, List( String ), Int, String));
      fun get_serv_ent NULL => NULL;
	 get_serv_ent (THE (name, aliases, port, protocol)) => THE (SERVENT {
	      name, aliases, port, protocol
	    } ); end;

      my get_server_by_name' : ((String, Null_Or( String )) ) -> Null_Or( Servent )
	 =
         netdb_fun "getServByName";

      my get_server_by_port' : ((Int, Null_Or( String )) ) -> Null_Or( Servent )
	 =
         netdb_fun "getServByPort";
    herein
	get_by_name = get_serv_ent o get_server_by_name';
	get_by_port = get_serv_ent o get_server_by_port';
    end; #  local 

};



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
