## cinterface.pkg

# Compiled by:
#     src/lib/std/src/stdlib-core.make6

stipulate
    package sg =  string_guts;			# string_guts	is from   src/lib/std/src/string-guts.pkg
herein
    package c_interface: Cinterface {		# Cinterface	is from   src/lib/std/src/unsafe/cinterface.api

						# inline_t	is from   src/lib/core/init/built-in.pkg

						# for assembly see
                                                #     src/lib/core/init/core.pkg
                                                #     src/lib/core/init/runtime-system-placeholder.pkg
                                                #     src/runtime/machine-dependent/X86.prim.asm

	Cfunction = assembly::a::Cfunction;

	bind_cfun = assembly::a::bind_cfun;

	exception  CFUN_NOT_FOUND String;

						# inline_t		is from   src/lib/core/init/built-in.pkg	

	fun c_function module_name fun_name
	    =
	    {   cfun =  bind_cfun (module_name, fun_name);

		if (inline_t::cast cfun  !=  0)
		    #		    
		    fn x =  (assembly::a::callc (cfun, x));
		else
		    raise exception  CFUN_NOT_FOUND (sg::cat [module_name, "::", fun_name]);
		fi;
	    };


	System_Constant
            =
            (Int, String);

	exception  SYSTEM_CONSTANT_NOT_FOUND String;

	# Linear scan down list,
	# checking for string equality
	# on (#2 listelement):
	#
	fun find_system_constant (name, l)
	    =
	    get l
	    where
		fun get []
			=>
			NULL;

		    get ((system_constant:  System_Constant) ! r)
			=>
			if (#2 system_constant  ==  name)
			    #  
			    THE system_constant;
			else
			    get r;
			fi;
		end;
	    end;

	fun bind_system_constant (name, l)
	    =
	    case (find_system_constant (name, l))
		#	      
		THE sc =>   sc;
		NULL   =>   raise exception  SYSTEM_CONSTANT_NOT_FOUND name;
	    esac;

    }; #  package c_interface 
end;



## COPYRIGHT (c) 1994 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
