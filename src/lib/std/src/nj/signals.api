## signals.api

# Compiled by:
#     src/lib/std/src/stdlib-core.make6



# These are the two basic interfaces to the run-time system signals support.
# The interface covers the basic signals operations, plus a small collection
# of standard signals that should be portable to non-UNIX systems.

api Signals {

    eqtype Signal;

    Signal_Action
      = IGNORE
      | DEFAULT
      | HANDLER  (Signal, Int, fate::Fate(Void))  ->  fate::Fate( Void )
      ;

    list_signals:  Void -> List( Signal );
	#
	# List the signals supported by this version 

    to_string:  Signal -> String;
	#  return the name of a signal 

    from_string:  String -> Null_Or( Signal );
	#
	# Return the signal with the corresponding name.
	# Return NULL if no such signal exists.

    set_signal_handler:       (Signal, Signal_Action) -> Signal_Action;
	#
	# Set the handler for a signal, returning the previous action. 

    override_signal_handler:  (Signal, Signal_Action) -> Signal_Action;
	#
	# If a signal is not being ignored, then set the handler.
	#
	# Returns the previous handler:  If IGNORE, then
	# the current handler is still IGNORE.

    get_signal_handler				# Get the current action for the given signal 
         :
         Signal -> Signal_Action;

    Signal_Mask
      = MASKALL
      | MASK  List(Signal)
      ;

    mask_signals:  Signal_Mask -> Void;
	#
	# Mask the specified set of signals.
	#
        # Signals that are not IGNORED
	# will be delivered when unmasked.
	#
	# Calls to mask_signals nest on a
	# per-signal basis.

    unmask_signals:  Signal_Mask -> Void;
	#
	# Unmask the specified signals.  The unmasking of a signal that is
	# not masked has no effect.

    masked:  Void -> Signal_Mask;
	#
	# return the set of masked signals; the value MASK[] means that
	# no signals are masked.

    pause:  Void -> Void;
	#
	# sleep until the next signal; if called when signals are masked,
	# then signals will still be masked when pause returns.

    # These signals should be supported even on non-UNIX platforms. 
    #
    interrupt_signal:            Signal;		# interactive interrupt 
    alarm_signal:                Signal;		# interval timer signal 
    terminal_signal:             Signal;		# termination 
    garbage_collection_signal:   Signal;		# garbage collection 

};							# api Signals 



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
