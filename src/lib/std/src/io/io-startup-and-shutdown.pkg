## io-startup-and-shutdown.pkg
## COPYRIGHT (c) 1996 AT&T Research.
#
# This module keeps track of open I/O streams
# and handles the proper cleaning of them.
#
# It is a modified version of the standard.lib package
#
#     src/lib/std/src/io/io-startup-and-shutdown--premicrothread.pkg
#
# Unlike the standard.lib version we only do cleanup
# at shutdown/exit time:  We do not try to support the
# persistence of threadkit streams across invocations
# of run_threadkit::run_threadkit).
#
# Also, we only require a single clean-up function, which
# flushes the standard streams and closes all others.
#
# These operations should only be called while threadkit
# is running, since they use synchronization primitives.
#
# NOTE: There is currently a problem with removing the
# cleaners for streams that get dropped by the application,
# but the system limit on open files will limit this.

# Compiled by:
#     src/lib/std/standard.lib




stipulate
    package ssh =  run_at; 					# run_at				is from   src/lib/src/lib/thread-kit/src/core-thread-kit/run-at.pkg
    package md  =  maildrop;					# maildrop				is from   src/lib/src/lib/thread-kit/src/core-thread-kit/maildrop.pkg
    package mps =  microthread_preemptive_scheduler;
herein

    package io_startup_and_shutdown
    :       Io_Startup_And_Shutdown				# Io_Startup_And_Shutdown		is from   src/lib/std/src/io/io-startup-and-shutdown.api
    {
	Tag = Ref( Void );

	Cleaner = { tag:    Tag,				# Unique ID for this cleaner. 
		    close:  Void -> Void			# Called SHUTDOWN and THREADKIT_SHUTDOWN. 
		  };

	std_stream_hook =  REF (fn () = ());

	cleaners = md::make_maildrop ([] : List( Cleaner ));

	fun note_stream_startup_and_shutdown_actions close
	    =
	    {   tag = REF();
		cleaner_rec = { tag, close };

		md::put_in_maildrop (cleaners, cleaner_rec ! md::take_from_maildrop cleaners);
		tag;
	    };

	fun get_tag ( { tag, ... } : Cleaner)
	    =
	    tag;

	fun change_stream_startup_and_shutdown_actions (t, close)
	    =
	    md::put_in_maildrop (cleaners, f (md::take_from_maildrop cleaners))
	    where

		fun f []
			=>
			raise exception FAIL "change_stream_startup_and_shutdown_actions: tag not found";

		    f (x ! r)
			=>
			{   t' = get_tag x;

			    if   (t' == t)

				 { tag=>t, close } ! r;
			    else
				 x ! f r;
			    fi;
			};
		end;
	    end;

	fun drop_stream_startup_and_shutdown_actions t
	    =
	    md::put_in_maildrop (cleaners, f (md::take_from_maildrop cleaners))
	    where
		fun f []      =>  [];				# Should we raise an exception here? 
		    f (x ! r) =>  if (get_tag x == t)   r;
				  else                  x ! f r;
				  fi;
		end;
	    end;

	fun do_closes ()
	    =
	    do_closes' (md::get_from_maildrop cleaners)
	    where

		fun do_closes' []
			=>
			();

		    do_closes' ({ tag, close }  !  rest)
			=>
			{   close ()   except   _ = ();
			    #
			    do_closes'  rest;
			};
		end;
	    end;


	fun clean_up  ssh::APP_SHUTDOWN
		=>
		{
# printf "clean_up(APP_SHUTDOWN) / AAA:  do_closes()        -- io-startup-and-shutdown.pkg\n";
		    do_closes ();
# printf "clean_up(APP_SHUTDOWN) / ZZZ        -- io-startup-and-shutdown.pkg\n";
		};

	    clean_up  ssh::THREADKIT_SHUTDOWN
		=>
		{
# printf "clean_up(THREADKIT_SHUTDOWN) / AAA:  do_closes()        -- io-startup-and-shutdown.pkg\n";
		    do_closes ();
# printf "clean_up(THREADKIT_SHUTDOWN) / ZZZ        -- io-startup-and-shutdown.pkg\n";
		};

	    clean_up  ssh::COMPILER_STARTUP
		=>
		{
		    *std_stream_hook ();
		};
	    clean_up  ssh::APP_STARTUP
		=>
		{
# printf "clean_up(APP_STARTUP) / AAA: *std_stream_hook ()        -- io-startup-and-shutdown.pkg\n";
		    *std_stream_hook ();
# printf "clean_up(APP_STARTUP) / ZZZ        -- io-startup-and-shutdown.pkg\n";
		};
	end;


	# Link master IO cleaner function
	# into the cleanup hook list:
	#
	io_cleaner
	    =
	    ( "io_cleaner",
              [ ssh::APP_SHUTDOWN,
                ssh::THREADKIT_SHUTDOWN,
                ssh::COMPILER_STARTUP,
                ssh::APP_STARTUP
              ],
              clean_up
            );

    };						# package threadkit_io_cleanup_at_shutodwn
end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1997 Bell Labs, Lucent Technologies.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
