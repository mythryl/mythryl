## freeclose.pkg 

# Compiled by:
#     src/lib/compiler/core.make6

###########################################################################
# 
# freemap_close
# 
#    Produces a free variable mapping at each function naming.
#    The mapping includes the functions bound at the			
#    MUTUALLY_RECURSIVE_FNS, but not the arguments of the function.                                           *
# 
#    Side-effect: all fundefs that are never referenced are removed
# 
###########################################################################

api Free_Close {

     Snum;       #  "stage_number" 
     Fvinfo;
     freemap_close
         :
         fate_passing_style::Function
         ->
         (  (  fate_passing_style::Function,
              (fate_passing_style::Highcode_Variable -> Snum),
              (fate_passing_style::Highcode_Variable -> Fvinfo),
              (fate_passing_style::Highcode_Variable -> Bool)
            )
         );
};

package free_close: (weak)  Free_Close {			# Free_Close	is from   src/lib/compiler/backend/upper/closures/freeclose.pkg

    stipulate

	include var_home;
	include fate_passing_style;
	include sorted_list;

	package lv= highcode_var;				# highcode_var	is from   src/lib/compiler/backend/upper/highcode/highcode-var.pkg

	package intset {

	    fun new ()      =      REF int_red_black_set::empty;
	    fun add set i   =   set := int_red_black_set::add    (*set, i);
	    fun mem set i   =          int_red_black_set::member (*set, i);
	    fun rmv set i   =   set := int_red_black_set::delete (*set, i);
	};

    herein

	package is= int_red_black_set;	# int_red_black_set	is from   src/lib/src/int-red-black-set.pkg
	package im= int_red_black_map;	# int_red_black_map	is from   src/lib/src/int-red-black-map.pkg

	package nd {
	    Key = Int;
	    compare = int::compare;
	};

	package scc			# "scc" == "strongly connected components"
            =
            digraph_strongly_connected_components_g( nd );

	# *************************************************************************
	#  Misc and utility functions                                             *
	# *************************************************************************
	say = controls::print::say;

	fun vp v
            =
            say (lv::name_of_highcode_variable v);

	fun addv_l (v, NULL ) =>   NULL;
	    addv_l (v, THE l) =>   THE (enter (v, l));
        end;

	enter
            =
            fn (VAR x, y) => enter (x, y);
               (    _, y) => y;
            end;

	error   =   error_message::impossible;

	fun warn s   =   ();          #  Apply say ["WARNING: ", s, "\n"] 

	fun add_l (v, NULL )   =>   NULL;
	    add_l (v, THE l)   =>   THE (enter (v, l));
        end;

	fun over_l (r, NULL )   =>   NULL;
	    over_l (r, THE l)   =>   THE (merge (r, l));
        end;

	fun merge_l (NULL,  r     )   =>   r;
	    merge_l (    l, NULL  )   =>   l;
	    merge_l (THE l, THE r )   =>   THE (merge (l, r));
        end;

	fun remove_l (vl, NULL  )   =>   NULL;
	    remove_l (vl, THE r)   =>   THE (remove (vl, r));
        end;

	fun rmv_l (v, NULL  )   => NULL; 
	    rmv_l (v, THE r)   => THE (rmv (v, r));
        end;

	fun clean l
            = 
            vars (NIL, l)
            where
	        fun vars (l, (VAR x) ! rest)   =>   vars (x ! l, rest);
		    vars (l,      _  ! rest)   =>   vars (   l, rest);
		    vars (l,            NIL)   =>   uniq l;
                end;
	    end;

	fun filter p vl
            = 
            f (vl, [])
            where
	        fun f (   [], l) =>   reverse l;
                    f (x ! r, l) =>   p x   ??   f (r, x ! l)
                                            ::   f (r,     l);
		end;
		    
	    end;

	fun exists prior l
            = 
            f l
            where
	        fun f      []   =>   FALSE;
		    f (a ! r)   =>   prior a   ??   TRUE
                                               ::   f r;
                end;
	    end;

	fun partition f l
            = 
	    fold_right
                (   fn (e, (a, b))
                       =
                       f e   ??   (e ! a,     b)
                             ::   (    a, e ! b)
                )
                ([], [])
                l;

	infinity = 1000000000;

	fun minl l
            = 
            f (infinity, l)
            where
	        fun f (i,   NIL) =>  i; 
		    f (i, j ! r) =>  i < j   ??   f (i, r)
                                             ::   f (j, r);
                end;
	    end;

	fun bfirst (p::BOXED | p::PNEQ | p::STRNEQ | p::CMP { oper => p::NEQ, ... } )   =>   TRUE;
	    bfirst _                                                                    =>   FALSE;
        end;

	fun bsecond (p::UNBOXED | p::PEQL | p::STREQ | p::CMP { oper => p::EQL, ... } ) => TRUE;
	    bsecond _                                                                   => FALSE;
        end;



	#  Datatype used to represent the free variable information: 

	Vnum = (Highcode_Variable, Int, Int);	# highcode_variable and first-use-sn and last-use-sn 
	Snum = Int;				# "stage_number" 

	Loopv = Null_Or( List( Highcode_Variable ) );

	Fvinfo
            =
            { fv:    List( Vnum ),		# List of sorted free variables.
	      lv:    Loopv,			# List of free variables on the loop path.
	      size:  (Int, Int)			# Estimated frame-size of the current fun.
            };

	fun freemap_close fe
            =
            {   # **************************************************************************
		#  Modify the fun_kind information for each fundef, new kind includes,     *
		#                                                                          *
		#       (1) KNOWN_CONT       known fate function                   *
		#       (2) KNOWN_TAIL       known tail-recursive function                 *
		#       (3) KNOWN            general known function                        *
		#       (4) CONT             general fate function                 *
		#       (5) ESCAPE           general escaping user function                *
		#       (6) KNOWN_REC        mutually recursive known function             *
		#                                                                          *
		# **************************************************************************

		escapes  = intset::new();
		escapes_p = intset::mem escapes;

		fun escapes_m (VAR v)   =>   intset::add escapes v;
		    escapes_m _         =>   ();
                end;

		users    =   intset::new();
		users_p   =   intset::mem users;
		users_m   =   intset::add users;

		known    =   intset::new();
		known_p   =   intset::mem known;
		known_m   =   intset::add known;

		fun known_k k   =   (k != FATE)   and   (k != ESCAPE    );
		fun frmsz_k k   =   (k == FATE)   or    (k == KNOWN_TAIL);

		contset   =   intset::new();
		cont_p     =   intset::mem contset;
		cont_m     =   intset::add contset;

		fun cont_k  k   =   (k == FATE)   or   (k == KNOWN_FATE);		#  Fate funs ?          
		fun econt_k k   =   (k == FATE);                          	#  Escaping fate funs ? 

		fun fixkind (fe as (FATE, f, vl, cl, ce))
			=> 
			if   (escapes_p f)
			      cont_m f;   fe;
			else  known_m f;  cont_m f;  (KNOWN_FATE, f, vl, cl, ce);
                        fi;

		    fixkind (fe as (fk, f, vl, cl as (cntt ! _), ce))
			=> 
			if   (escapes_p f)
			       users_m  f; (ESCAPE,          f, vl, cl, ce);
			else   known_m  f; (KNOWN_RECURSIVE, f, vl, cl, ce);
                        fi;

		    fixkind (fe as (fk, f, vl, cl, ce))
			=> 
			if   (escapes_p f)
			    
			     vp f;
			     say " ***** \n";
			     error "escaping-fun has zero fate, freeclose.pkg";
			else
			     known_m f;
			     (KNOWN_TAIL, f, vl, cl, ce);
			fi;
                end;

		fun procfix (fk, f, vl, cl, ce)
                    =
                    (fk, f, vl, cl, proc ce)

		also
                fun proc  ce
                    =
		    case ce 
                      
		        MUTUALLY_RECURSIVE_FNS (fl, body)
			    =>
			    {   body' =   proc body;

				nfl   =   map fixkind (map procfix fl);

				# Due to possible eta-splits of fate functions, 
				# since it's always that FATE funs call KNOWN_FATE funs, 
				# we split them into two MUTUALLY_RECURSIVE_FNSes, so that each MUTUALLY_RECURSIVE_FNS only 
				# contains at most one fate definition.


				my (fl1, fl2)
				    =
				    partition (econt_k o #1) nfl; 

				case (fl1, fl2) 
				     ( [],   _)   =>   MUTUALLY_RECURSIVE_FNS (fl2, body');
				     (  _,  [])   =>   MUTUALLY_RECURSIVE_FNS (fl1, body');
				     _            =>   MUTUALLY_RECURSIVE_FNS (fl2, MUTUALLY_RECURSIVE_FNS (fl1, body'));
                                esac;
			    };

		        APPLY (v, args)
			    =>
			    {   apply escapes_m args;
				ce;
			    };

		        SWITCH (v, c, l)
			    =>
			    SWITCH (v, c, map proc l);

		        RECORD (rk, l, w, ce)
			    => 
			    {   apply (escapes_m o #1) l;
				RECORD (rk, l, w, proc ce);
			    };

		        SELECT (i, v, w, t, ce)
			    =>
			    SELECT (i, v, w, t, proc ce);

		        OFFSET (i, v, w, ce)
			    =>
			    OFFSET (i, v, w, proc ce);

		        LOOKER (p, vl, w, t, ce)
			    => 
			    {   apply escapes_m vl;
				LOOKER (p, vl, w, t, proc ce);
			    };

		        ARITH (p, vl, w, t, ce)
			    => 
			    {   apply escapes_m vl;
				ARITH (p, vl, w, t, proc ce);
			    };

		        PURE (p, vl, w, t, ce)
			    => 
			    {   apply escapes_m vl;
				PURE (p, vl, w, t, proc ce);
			    };

		        SETTER (p, vl, ce)
			    => 
			    {   apply escapes_m vl;
				SETTER (p, vl, proc ce);
			    };

		        RAW_C_CALL (k, l, p, vl, wtl, ce)
			    =>
			    {   apply escapes_m vl;
				RAW_C_CALL (k, l, p, vl, wtl, proc ce);
			    };

		        BRANCH (p, vl, c, e1, e2)
			    =>
			    {   apply escapes_m vl;
				BRANCH (p, vl, c, proc e1, proc e2);
			    };
                    esac;

		fe'   =   procfix fe;




		# *************************************************************************
		# Build the call graph and compute the scc number                         *
		# *************************************************************************

		fun kuc x
                    =
                    (cont_p  x)   or
                    (known_p x)   or
                    (users_p x);

		fun make_graph f
                    =
                    {   fun comb  ((xe, xf), (ye, yf))   =   (is::union (xe, ye), xf @ yf);
			fun combe ((xe, xf),        e)   =   (is::union (xe, e),  xf     );
			fun combf ((xe, xf),        f)   =   (xe, xf @ f);

			fun add_kuc (s, v)
                            =
                            if   (kuc v   )   is::add (s, v);
                                         else   s;   fi;

			fun vl2s_kuc l
                            =
			    loop (l, is::empty)
                            where
                                fun loop (       [], s) =>   s;
				    loop (VAR v ! r, s) =>   loop (r, add_kuc (s, v));
				    loop (    _ ! r, s) =>   loop (r, s);
                                end;
			    end;

			fun collect (SWITCH (_, _, el))
				=>
				fold_left
				    (   fn (x, a)
					   =
					   comb (collect x, a)
				    )
				    (is::empty, [])
				    el;

			   collect (SETTER (p::SETHANDLER, vl, e))
                               =>
                               combe (collect e, vl2s_kuc vl);

			   collect ( RECORD (   _, _, _, e)
				   | SELECT (_, _, _, _, e)
				   | OFFSET (   _, _, _, e)
				   | SETTER (      _, _, e)
				   | LOOKER (_, _, _, _, e)
				   | ARITH  (_, _, _, _, e)
				   | PURE   (_, _, _, _, e)
				   | RAW_C_CALL (_, _, _, _, _, e)
                                   )   =>
                                       collect e;

			   collect (BRANCH (_, _, _, x, y))
                               =>
                               comb (collect x, collect y);

			   collect (APPLY (u, ul))
                               =>
                               (vl2s_kuc (u ! ul), []);

			   collect (MUTUALLY_RECURSIVE_FNS (fl, b))
                               =>
                               combf (collect b, fl);
                        end;

			fun dofun ((_, f, _, _, body), (m, all))
                            =
                            {   my (es, fl)   =   collect body;
				m'         =   im::set (m, f, is::vals_list es);
				all'       =   is::add (all, f);
			    
				fold_left
                                    dofun
                                    (m', all')
                                    fl;
			    };

			my (follow_map, allset)
                            =
                            dofun (f, (im::empty, is::empty));

			rootedges   =   is::vals_list allset;

			fun follow v
                            =
                            the (im::get (follow_map, v));
		    
			{ roots  => rootedges,
                          follow
                        };
		    };

		fun ass_num (scc::SIMPLE v, (i, nm))
			=>
			( i + 1,
			  im::set (nm, v, i)
			);

		    ass_num (scc::RECURSIVE vl, (i, nm))
			=>
			(   i + 1,

			    fold_left
				(fn (v, nm) =  im::set (nm, v, i))
				nm
				vl
			);
                end;

		number_map
                    =
                    #2 (fold_left ass_num (0, im::empty) (scc::topological_order' (make_graph fe')));

		fun sccnum x
                    =
                    the (im::get (number_map, x));

                                #  Why is this stuff all commented out? -- CrT XXX BUGGO FIXME 
				/*
				exception Unseen
				type info = { dfsnum:  Ref( Int ), sccnum:  Ref( Int ), edges:  List( Highcode_Variable ) }

				my m:  int_hash_table::Hash_Table( info )
                                     = int_hash_table::make_table (32, Unseen)

				lookup = int_hash_table::lookup m
				my total:  Ref( List( Highcode_Variable ) ) = REF NIL

				fun addinfo (f, vl) =
				    (total := (f ! *total);
				     int_hash_table::set m (f,{ dfsnum=REF -1, sccnum=REF -1, edges=vl } ))
				fun kuc x = (contP x) or (knownP x) or (usersP x)
#				fun ec x = (contP x) or (escapesP x)

				fun makenode (_, f, _, _, body)
                                  =
				  let fun edges (RECORD(_, _, _, e)) = edges e
					| edges (SELECT(_, _, _, _, e)) = edges e
					| edges (OFFSET(_, _, _, e)) = edges e
					| edges (SWITCH(_, _, el)) = foldmerge (map edges el) 
					| edges (SETTER (p::SETHANDLER, vl, e)) = 
					    merge (filter kuc (clean vl), edges e)
					| edges (SETTER(_, _, e)) = edges e
					| edges (LOOKER(_, _, _, _, e)) = edges e
					| edges (ARITH(_, _, _, _, e)) = edges e
					| edges (PURE(_, _, _, _, e)) = edges e
					| edges (BRANCH(_, _, _, a, b)) = merge (edges a, edges b)
					| edges (APPLY (u, ul)) = filter kuc (clean (u ! ul))
					| edges (MUTUALLY_RECURSIVE_FNS (fl, b)) = (apply makenode fl; edges b)
				   in addinfo (f, edges body)
				  end 

				compnums = REF 0 and id = REF 0
				my stack:   Ref( List( Int * Ref( Int ) ) ) = REF NIL
				fun scc nodenum =
				  let fun newcomp (c, (n, sccnum) ! rest) = 
					    (sccnum := c; 
					     if n==nodenum then rest else newcomp (c, rest))
					| newcomp _ = error "newcomp in freeclose in the closure phase"

				      my info as { dfsnum as REF d, sccnum, edges } = lookup nodenum

				   in if d >= 0 then if  *sccnum >= 0  then infinity else d 
				      else (let v = *id before (id := *id+1)
						(stack := (nodenum, sccnum) ! *stack;
							 dfsnum := v)
						b = minl (map scc edges)
					     in if v <= b 
						then let c = *compnums before (compnums := *compnums+1)
							 (stack := newcomp (c,*stack))
						      in infinity #  v 
						     end
						else b
					    end)
				  end

				makenode (fe')               #  Build the call graph 
				apply (fn x => (scc x; ())) *total   #  Compute the scc number 
				sccnum = ! o .sccnum o lookup
				*/
		    


		fun samescc (x, n)			# "scc" == "strongly connected component"
                    =
                    n < 0   ??   FALSE
                            ::   sccnum x  ==  n;



		/*** >>
		   fun plist p l = (apply (fn v => (say " "; p v)) l; say "\n")
		   ilist = plist vp
		   apply (fn v => (vp v; say " edges:  " ;
					 ilist(.edges (lookup v));
					 say "****   sccnum is   "; 
					 say (int::to_string (sccnum v)); say "\n")) *total
		<<***/



		# *************************************************************************
		# Utility functions for lists of free variable unit.                      *
                # Each unit "vnum" contains three parts:                                  *
                #     the Highcode_Variable,                                                *
                #     the first-use-sn  and                                               *
                #     the last-use-sn                                                     *
		# *************************************************************************
		#
		v2l = map h            #  Given a vnum list, return an Highcode_Variable List
                      where
                          fun h (s: Vnum)   =   #1 s; 
		      end;	  



		#  Add a single Highcode_Variable used at stage n 
		#
		fun adds_v (VAR v, n, l)
			=> 
			h (v, l)
                        where
			    fun h (v, [])
                                    =>
                                    [ (v, n, n) ];

				h (v, l as ((u as (x, a, b)) ! r))
				    => 
				    if   (x <  v)   u ! (h (v, r));
				    elif (x == v)  ((x, int::min (a, n), int::max (a, n)) ! r);
				    else           ((v, n, n) ! l);
				    fi;
			    end;
			end;

		    adds_v (_, _, l)
                        =>
                        l;
                end;



		# Remove a single Highcode_Variable: 
		#
		fun rmvs_v (v, [])
                        =>
                        [];

		    rmvs_v (v, l as ((u as (x, _, _)) ! r))
			=> 
			if   (x <  v)   u ! (rmvs_v (v, r));
			elif (x == v)   r;
			else            l;
                        fi;
                end;



		# Remove a list of lvars: 
		#
		fun remove_v (vl, l)
                    = 
                    h (vl, l)
                    where
		        fun h   ( l1 as (x1 ! r1),
                                  l2 as ((u2 as (x2, _, _)) ! r2)
                                )
				=> 
				if   (x2 < x1)   u2 ! (h (l1, r2));
				elif (x2 > x1)         h (r1, l2);
				else		       h (r1, r2);
			        fi;

			    h ([], l2)   =>   l2;
			    h (l1, [])   =>   [];
                        end;
		    end;



		# Add a list of lvars used at stage n: 
		#
		fun add_v (vl, n, l)
                    = 
                    h (vl, l)
                    where
		        fun h (   l1 as (x1 ! r1),
                                 l2 as ((u2 as (x2, a2, b2)) ! r2)
                              )
				=>
				if   (x1 < x2)    (x1, n, n) ! (h (r1, l2));
				elif (x1 > x2)            u2 ! (h (l1, r2));
				else              (x1, int::min (n, a2), int::max (n, b2))   !   (h (r1, r2));
				fi;

			    h( l1,[])   =>   map   (fn x =  (x, n, n))   l1;
			    h ([], l2)  =>   l2;
                        end;
		    end;



		#  Merge two lists of free var unit (exclusively) 
		#
		fun merge_pv (n, l1, l2)
                    = 
                    h (l1, l2)
                    where
		        fun h (   l1 as ((x1, a1, b1) ! r1),
                                  l2 as ((x2, a2, b2) ! r2)
                              )
                            =>
			    if   (x1  < x2)   (x1, n, n) ! (h (r1, l2));
			    elif (x1  > x2)   (x2, n, n)  !  (h (l1, r2));
			    elif (b1 == b2)   (x1, int::min (a1, a2), b1)  !  (h (r1, r2));
			    else              (x1, n, n)  !  (h (r1, r2));
                            fi;

			    h (l1, []) =>   map   (fn (x, _, _) =  (x, n, n))   l1;
			    h ([], l2) =>   map   (fn (x, _, _) =  (x, n, n))   l2;
                        end;
		    end;



		# Merge two lists of free var unit (with union) 
		#
		fun merge_uv (  l1:   List( (Highcode_Variable, Int, Int) ),
                                l2
                             )
                    =
                    h (l1, l2)
                    where
		        fun h   ( l1 as ((u1 as (x1, a1, b1)) ! r1),
                                  l2 as ((u2 as (x2, a2, b2)) ! r2)
                                )
				=>
				if   (x1 < x2)   u1 ! (h (r1, l2));
				elif (x1 > x2)   u2 ! (h (l1, r2));
				else	         (x1, int::min (a1, a2), int::max (b1, b2))  !  (h (r1, r2));
				fi;

			   h (l1, []) =>   l1;
			   h ([], l2) =>   l2;
                        end;
		    end;



		# Fold merge lists of free vars (exclusively) 
		#
		fun fold_uv (l, b)
                    =
                    fold_right merge_uv b l;



		# Lay a list of free var unit over
                # another list of free var unit:
		#
		fun over_v (n, l1, l2)
                    = 
                    h (l1, l2)
                    where
		        fun h (   l1 as ((u1 as (x1, _, _)) ! r1),
                                  l2 as (       (x2, _, _) ! r2)
                              )
			       =>
			       if    (x1 < x2)          u1 ! (h (r1, l2));
			       elif  (x1 > x2)  (x2, n, n) ! (h (l1, r2));
			       else                     u1 ! (h (r1, r2));
			       fi;

			    h (l1, [])   =>   l1;
			    h ([], l2)   =>   map   (fn (x, _, _) =  (x, n, n))   l2;
                        end;
		    end;



		# **************************************************************************
		# Two hash tables (1) highcode_variable to stage number                      *
		#                 (2) highcode_variable to freevar information               *
		# **************************************************************************

		exception STAGENUM;

		my snum:      int_hash_table::Hash_Table( Snum )     #  "snum" = "stageNumber"  
                    =
                    int_hash_table::make_table (32, STAGENUM);

		addsn   =   int_hash_table::set   snum;	#  Add the stage number for a fundef. 
		getsn   =   int_hash_table::get  snum;	#  Get the stage number of a fundef.  

		fun findsn (v, d, [])
                    =>
                    {   warn (   "Fundef "
                             +   (lv::name_of_highcode_variable v)
			     +   " unused in freeClose"
                             );
                        d;
                    };

		   findsn (v, d, (x, _, m) ! r)
                    => 
		    if   (v > x)
                        
                         findsn (v, d, r); 
		    else
                         if   (v == x)
                              m; 
			 else
                              warn (   "Fundef "
                                       +   (lv::name_of_highcode_variable v)
                                       +   " unused in freeClose"
                                       );
                                  d;
                         fi;
                    fi;
                end;

		fun findsn2 (v, d, [])
                        =>
                        d;

		    findsn2 (v, d, (x, _, m) ! r)
			=> 
			if   (v  > x)   findsn2 (v, d, r);
			elif (v == x)   m;
			else            d;
			fi;
                end;



		exception FREEVMAP;

		my vars:  int_hash_table::Hash_Table( Fvinfo )
                    =
                    int_hash_table::make_table (32, FREEVMAP);

		fun add_entry (v, l, x, s)
                    =
                    int_hash_table::set vars (v, { fv=>l, lv=>x, size=>s } );

		free_v =   int_hash_table::get  vars;    #  Get the freevar info.                
		loop_v =   .lv o free_v;                     #  The free variables on the loop path. 

		/*** >>
		  my vars:  int_hash_table::Hash_Table( List( Highcode_Variable ) * Null_Or( List( Highcode_Variable ) ) )
							   = int_hash_table::make_table (32, FREEVMAP)
		  freeV = int_hash_table::lookup vars 
		  fun loopV v = (#2 (freeV v)) except FREEVMAP => error "loopV in closure"
		<<***/



		# *************************************************************************
		# Split the pseudo-mutually-recursive namings, a temporary hack.         *
		#                                                                         *
		# TODO: need to add code on identify those KNOWN_REC kind functions       *
		#       check the older version of this file for details                  *
		#       XXX BUGGO FIXME                                                   *
		# *************************************************************************

		fun known_opt ([], _, _, _, _)
                        =>
                        error "knownOpt in closure 4354";

		    known_opt (flinfo, died, freeb, gszb, fszb)
			=> 
			{   newflinfo
				= 
				{   roots   =   filter (member died) (v2l freeb);

				    graph
					=
					map
					    (   fn ((_, f, _, _, _), free, _, _)
						   =
						   (   f,
						       filter (member died) (v2l free)
						   )
					    )

					    flinfo;

				    fun loop (old)
					= 
					{   new
						= 
						fold_right
						    (   fn ((f, free), total)
							   =
							   member old f   ??   merge (free, total)
							                  ::   total
						    )
						    old
						    graph;


					    if   (length new == length old)
					        
						 new;
					    else
						 loop new;
                                            fi;
					};

				    nroots   =   loop roots;


				    filter
					(fn ((_, f, _, _, _), _, _, _) =  member nroots f)
					flinfo;
				};

			    my (nfl, freel, gsz, fsz)
				=
				{   my (known, other)
					= 
					partition
					    fn ((KNOWN_RECURSIVE, _, _, _, _), _, _, _) => TRUE;
					      _                                         => FALSE;
					    end 

					    newflinfo;

				  known'
				      = 
				      case known 

					   u as [ ((_, v, args, cl, body), free, gsz, fsz) ]
					       => 
					       if   (member (v2l free) v)
					           
						    u;
					       else
						    [ ((KNOWN, v, args, cl, body), free, gsz, fsz) ];
                                               fi;

					  z => z;
                                      esac;

				  fun g ( (fe, vn, gsz', fsz'), (fl, vl, gsz, fsz))
				      =
				      (fe ! fl, vn ! vl, int::max (gsz', gsz), int::max (fsz', fsz));

				  fold_right   g   ([], [], gszb, fszb)   (known' @ other);
			      };

			    header
				=
				case nfl
				    []   =>   (fn ce = ce            );
				     _   =>   (fn ce = MUTUALLY_RECURSIVE_FNS (nfl, ce) );
				esac;


			    ( header,
			      freel,
			      gsz,
			      fsz
			    );
			};
                    end;



		# *************************************************************************
		# The following procedure does five things:                               *
		#                                                                         *
		#  (1) Install a stage number for each function definition                *
		#  (2) Collect the free variable information for each fundef              *
		#  (3) Infer the live range of each free variable at each fundef          *
		#  (4) Infer the set of free variables on the looping path                *
		#  (5) Do the simple branch-prediction transformation                     *
		#                                                                         *
		# TODO: better branch-prediction heuristics will help the merge done      *
		#       at each SWITCH and BRANCH    XXX BUGGO FIXME                      *
		# *************************************************************************

		#  Major gross hack here: 

		ekfuns    =   intset::new();
		ekfuns_p   =   intset::mem ekfuns;
		ekfuns_m   =   intset::add ekfuns;

		fun freefix
                    (sn, freeb)
                    (fk, f, vl, cl, ce)
                    =
		    {   my (ce', ul, wl, gsz, fsz)
                            = 
			    if   (cont_k fk)
                                 
				 n  = findsn (f, sn, freeb);

				 nn = econt_k fk   ??   n+1
                                                   ::   n;

				 addsn (f, nn);
				 freevars (sccnum f, nn, ce); 

			    else if   (known_k fk)
                                     
                                        addsn (f, sn  );   freevars (sccnum f, sn,   ce);
				 else   addsn (f, sn+1);   freevars (      -1, sn+1, ce);
                                 fi;
                            fi;

			args   =   uniq vl;

			l   =   remove_v (args, ul);
			z   =   remove_l (args, wl);

			#  The following is a gross hack,
                        # needs more work XXX BUGGO FIXME 
			#
			nl  = 
			    if   ((findsn2 (f, sn, l)) <= sn)
                                 l;
			    else
				 fold_right
				    (fn ((x, i, j), z)
					=
					{   if   (known_p x)   ekfuns_m x;   fi; 
					    (x, i+1, j+1)  !  z;
					}
				    )
				    []
				    l;

                            fi;
			                                                
                        add_entry (f, l, z, (gsz, fsz));

			my (gsz', fsz')
                            = 
			    if (frmsz_k fk)     #  Only count escap-fate & knowntail funs 

			          gn = length l;       # *** NEED MORE WORK HERE XXX BUGGO FIXME ***
				      
                                  ( int::max (gn, gsz),
                                    fsz
                                  );
			    else
                                  (0, 0);
                            fi;

		    
                        ( (fk, f, vl, cl, ce'),
                          nl,
                          gsz',
                          fsz'
                        );
		    }

		also fun freevars (n, sn, ce)
                    =
		    case ce 

		         MUTUALLY_RECURSIVE_FNS (fl, body)
                         =>
			 {   died   =   uniq (map #2 fl); 

			     my (body', freeb, wl, gszb, fszb)
                                 =
                                 freevars (n, sn, body);

			     flinfo
                                 =
                                 map   (freefix (sn, freeb))   fl;

			     my (header, freel, gsz, fsz)
                                 =
                                 known_opt (flinfo, died, freeb, gszb, fszb);

			     free
                                 =
                                 remove_v (died, fold_uv (freel, freeb));

			     nwl
                                 =
                                 case wl 

			             NULL  => NULL;

				     THE l
					 => 
					 (   { fun h (x, l)
						     =
						     if   (member died x)
							  merge_l (loop_v x, l); 
						     else addv_l (x, l);fi; 

						 remove_l (died, fold_right h (THE []) l);
					     }
					 );
                                 esac;
			 
                             ( header (body'),
                               free,
                               nwl,
                               gsz,
                               fsz
                             );
			 };

		        APPLY (v, args)
			    => 
			    {   free = clean (v ! args);
				fns = filter kuc free;

				wl =    exists (fn x = samescc (x, n)) fns
                                            ??   THE free
					    ::    NULL;

				freeb = add_v (free, sn,[]);

				( ce,
				  freeb,
				  wl,
				  0,
				  0
				);
			    };

		        SWITCH (v, c, l)     #  Add branch prediction heauristics in the future 
			    =>
			    {   fun freelist (ce, (el, free1, free2, wl, gsz1, fsz1, gsz2, fsz2))
				    =
				    {   my (ce', free', wl', gsz', fsz')
					    =
					    freevars (n, sn, ce);

					case wl' 

					     NULL
						 => 
						 ( ce' ! el,
						   free1,
						   merge_pv (sn, free', free2),
						   wl,
						   gsz1,
						   fsz1,
						   int::max (gsz2, gsz'),
						   int::max (fsz2, fsz')
						 );

					     THE _
						 => 
						 ( ce' ! el,
						   merge_uv (free', free1),
						   free2,
						   merge_l (wl', wl),
						   int::max (gsz1, gsz'),
						   int::max (fsz1, fsz'),
						   gsz2,
						   fsz2
						 );
                                        esac;
				    };

				my (l', free1, free2, wl, gsz1, fsz1, gsz2, fsz2)
				    = 
				    fold_right   freelist   ([],[],[], NULL, 0, 0, 0, 0)   l;

				my (free, gsz, fsz)
				    =
				    case wl
				        NULL  => (                  free2,  gsz2, fsz2);
					THE _ => (over_v (sn, free1, free2), gsz1, fsz1);
                                    esac;

				( SWITCH (v, c, l'),
				  adds_v (v, sn, free),
				  add_l (v, wl),
                                  gsz,
                                  fsz
                                );
			    };

		   #     | SWITCH (v, c, l) =>  # XXX BUGGO FIXME add branch prediction heauristics in the future 
		   # 	  let fun freelist (ce, (el, free, wl, gsz, fsz)) =
		   # 		let my (ce', free', wl', gsz', fsz') = freevars (n, sn, ce)
		   # 		    ngsz = int::max (gsz, gsz')
		   # 		    nfsz = int::max (fsz, fsz')
		   # 		 in (ce' ! el, mergePV (sn, free', free), mergeL (wl', wl), ngsz, nfsz)
		   # 		end
		   # 	      my (l', freel, wl, gsz, fsz) = fold_right freelist ([],[], NULL, 0, 0) l
		   # 	   in (SWITCH (v, c, l'), addsV (v, sn, freel), addL (v, wl), gsz, fsz)
		   # 	  end


		        RECORD (rk, l, w, ce)
			    => 
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean (map #1 l);   

				free'
				    =
				    add_v (new, sn, rmvs_v (w, free));

				wl'
				    =
				    over_l (new, rmv_l (w, wl));


				( RECORD (rk, l, w, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        SELECT (i, v, w, t, ce)
			    =>
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				free' = adds_v (v, sn, rmvs_v (w, free));

				wl' = add_l (v, rmv_l (w, wl));

				( SELECT (i, v, w, t, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        OFFSET (i, v, w, ce)
			    =>
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				free' = adds_v (v, sn, rmvs_v (w, free));

				wl' = add_l (v, rmv_l (w, wl));

				( OFFSET (i, v, w, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        LOOKER (p, vl, w, t, ce)
			    => 
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				free' = add_v (new, sn, rmvs_v (w, free));

				wl' = over_l (new, rmv_l (w, wl));

				( LOOKER (p, vl, w, t, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        ARITH (p, vl, w, t, ce)
			    => 
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				free' = add_v (new, sn, rmvs_v (w, free));

				wl' = over_l (new, rmv_l (w, wl));

				( ARITH (p, vl, w, t, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        PURE (p, vl, w, t, ce)
			    => 
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				free' = add_v (new, sn, rmvs_v (w, free));

				wl' = over_l (new, rmv_l (w, wl));

				( PURE (p, vl, w, t, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        SETTER (p as p::SETHANDLER, vl, ce)
			    =>
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				free' = add_v (new, sn, free);

				fns = filter kuc new;

				wl' =   exists  (fn x =  samescc (x, n))  fns
					    ??   merge_l (THE new, wl)
					    ::   over_l (new, wl);

				( SETTER (p, vl, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        SETTER (p, vl, ce)
			    => 
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				free' = add_v (new, sn, free);

				wl' = over_l (new, wl);

				( SETTER (p, vl, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        RAW_C_CALL (k, l, p, vl, wtl, ce)
			    =>
			    {   my (ce', free, wl, gsz, fsz)
				    =
				    freevars (n, sn, ce);

				new = clean vl;

				wtl' = map #1 wtl;

				free' = add_v (new, sn, fold_left rmvs_v free wtl');

				wl' = over_l (new, fold_left rmv_l wl wtl');

				( RAW_C_CALL (k, l, p, vl, wtl, ce'),
				  free',
				  wl',
				  gsz,
				  fsz
				);
			    };

		        BRANCH (p, vl, c, e1, e2)
			    =>
			    {   my (e1', free1, wl1, gsz1, fsz1)   =   freevars (n, sn, e1);
				my (e2', free2, wl2, gsz2, fsz2)   =   freevars (n, sn, e2);

				new = clean vl;

				wl = over_l (new, merge_l (wl1, wl2));

				case (wl1, wl2)

				     (NULL, THE _)
				         => 
				         {   free = add_v (new, sn, over_v (sn, free2, free1));

					     ( BRANCH (p::opp p, vl, c, e2', e1'),
					       free,
					       wl,
					       gsz2,
					       fsz2
					     );
					 };

				    (THE _, NULL)
				        => 
					{   free = add_v (new, sn, over_v (sn, free1, free2));

					    ( BRANCH (p, vl, c, e1', e2'),
					      free,
					      wl,
					      gsz1,
					      fsz1
					    );
					};

				    _
				     => 
				        {   free
                                                =
                                                case wl1 

						    THE _
							=>
							add_v (new, sn, merge_uv (free1, free2));

						    _ => if   (bfirst p)    add_v (new, sn, over_v   (sn, free1, free2));
							 elif (bsecond p)   add_v (new, sn, over_v   (sn, free2, free1));
							 else               add_v (new, sn, merge_pv (sn, free1, free2));
							 fi;
						esac;

					     gsz   =   int::max (gsz1, gsz2);
					     fsz   =   int::max (fsz1, fsz2);

					     ( BRANCH (p, vl, c, e1', e2'),
					       free,
					       wl,
					       gsz,
					       fsz
					     );
                                        };
                                 esac;
			    };
                       esac;

	    
                ( #1 (freefix (0, []) fe'),
                  getsn,
                  free_v,
                  ekfuns_p
                );
	    };                                                 #  function freemap_close 


#	my freemapClose
#            =
#            compile_statistics::do_phase
#                (compile_statistics::make_phase "Compiler 079 freemapClose")
#	        freemapClose


    end;

};			#  package free_close 


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1996 by Bell Laboratories 
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
