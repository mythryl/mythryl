## sparc-freq-props.pkg

# Compiled by:
#     src/lib/compiler/backend/lower/make7/sparc.make6




# Extract frequency information from the sparc architecture
# 
# -- Allen Leung



###                 "If my impressions are correct, our educational planing mill
###                  cuts down all the knots of genius, and reduces the best
###                  of the men who go through it to much the same standard."
###
###                                                    -- Simon Newcomb 



generic package sparc_instruction_frequency_properties_g (

    instruction_set:  Instruction_Set_Sparc						# Instruction_Set_Sparc		is from   src/lib/compiler/backend/lower/sun/instruction/instruction-set-sparc.api
)

: (weak)  Frequency_Properties								# Frequency_Properties		is from   src/lib/compiler/backend/lower/instruction/freq-props.api

{
    package instruction_set = instruction_set;

    package i = instruction_set;

    p10 = probability::percent 10;
    p50 = probability::percent 50;
    p90 = probability::percent 90;
    p100 = probability::always;

    fun cond i::BA  => p100;
	cond i::BE  => p10;
	cond i::BNE => p90;
	cond _      => p50;
    end;

    fun fcond i::FBA  => p100;
	fcond i::FBE  => p10;
	fcond i::FBNE => p90;
	fcond _       => p50;
    end;

    fun sparc_branch_prob (i::BICC { b, ... } ) => cond b;
	sparc_branch_prob (i::FBFCC { b, ... } ) => fcond b;
	sparc_branch_prob (i::BP { b, ... } ) => cond b;
	sparc_branch_prob (i::BR _) => p50;
	sparc_branch_prob (i::JMP _) => p100;
	sparc_branch_prob (i::RET _) => p100;
	sparc_branch_prob _ => probability::never;
    end; #  non-branch 

    fun branch_prob (i::NOTE { note, instruction, ... } )
	    =>
	    case (lowcode_notes::branch_prob.peek note)
              
	         THE b =>   b;
	         NULL  =>   branch_prob instruction;
            esac;

	branch_prob (i::BASE_INSTRUCTION i) =>   sparc_branch_prob i;
	branch_prob _                       =>   probability::never;
    end;

};


## COPYRIGHT (c) 2002 Bell Labs, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
