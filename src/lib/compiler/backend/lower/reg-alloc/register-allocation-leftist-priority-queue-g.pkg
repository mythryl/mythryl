## register-allocation-priority-queue-g.pgk

# Compiled by:
#     src/lib/compiler/backend/lower/make7/lowcode.make6

# Priority Queue.  Let's hope the compiler will inline it for performance



###          "Everything that can be invented has been invented.
###
###                     -- Charles H. Duell, Commissioner, US Patent Office, 1899


# We are invoked from:
#
#     src/lib/compiler/backend/lower/reg-alloc/register-allocation-core.pkg

generic package
register_allocation_leftist_priority_queue_g (

    Element;

    less:  (Element, Element) -> Bool;
)

: (weak) Register_Allocation_Priority_Queue				# Register_Allocation_Priority_Queue	is from   src/lib/compiler/backend/lower/reg-alloc/register-allocation-priority-queue.api

=
package {

    # A leftist tree is a binary tree with priority ordering
    # with the invariant that the left branch is always the taller one         

    Element = Element;

    Priority_Queue = TREE  (Element, Int, Priority_Queue, Priority_Queue)
	      | EMPTY;

    fun merge'(EMPTY, EMPTY)
            =>
            (EMPTY, 0);

	merge'(EMPTY, a as TREE(_, d, _, _)) => (a, d);
	merge'(a as TREE(_, d, _, _), EMPTY) => (a, d);

	merge'(a as TREE (x, d, l, r), b as TREE (y, d', l', r'))
            =>
	    (TREE (root, d_t, l, r), d_t)
            where
	       my (root, l, r1, r2)
                    = 
		    if (less (x, y))   (x, l, r, b);
                    else               (y, l', r', a);
                    fi; 

	        my (r, d_r) = merge'(r1, r2);

	        d_l = case l    EMPTY            => 0;
                                TREE(_, d, _, _) => d;
                      esac; 

	        my (l, r, d_t)
                    =
                    if (d_l >= d_r)   (l, r, d_l+1);
                    else              (r, l, d_r+1);
                    fi;
            end;
    end;


    fun merge (a, b)
        =
        #1 (merge'(a, b));


    fun add (x, EMPTY)
            =>
            TREE (x, 1, EMPTY, EMPTY);

        add (x, b as TREE (y, d', l', r'))
            => 
            if (less (x, y))  TREE (x, d'+1, b, EMPTY);
            else              #1 (merge'(TREE (x, 1, EMPTY, EMPTY), b));
            fi;
    end;
};

