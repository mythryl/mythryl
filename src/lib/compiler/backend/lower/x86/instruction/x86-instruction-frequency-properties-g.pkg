## x86-instruction-frequency-properties-g.pkg

# Compiled by:
#     src/lib/compiler/backend/lower/make7/ia32.make6



# Extract frequency information from the X86 architecture
#
# -- Allen Leung



###                  "But the beauty of Einstein's equations,
###                   for example, is just as real to anyone who's
###                   experienced it as the beauty of music.
###
###                  "We've learned in the 20th century that
###                   the equations that work have inner harmony."
###
###                                       -- Edward Witten 



# We are nowhere invoked.

generic package
x86_instruction_frequency_properties_g (

    instruction_set:  Instruction_Set_X86					# Instruction_Set_X86	is from   src/lib/compiler/backend/lower/x86/instruction/instruction-set-x86.api
)

: (weak) Frequency_Properties							# Frequency_Properties	is from   src/lib/compiler/backend/lower/instruction/freq-props.api

=
package {

    package instruction_set = instruction_set;
    package i               = instruction_set;

    p0_001 = probability::prob (1, 1000);
    p10    = probability::percent 10;
    p50    = probability::percent 50;
    p90    = probability::percent 90;
    p100   = probability::always;

    fun x86branch_prob (i::JCC { cond => i::EQ, ... } ) => p10;
	x86branch_prob (i::JCC { cond => i::OO, ... } ) => p0_001;
	x86branch_prob (i::JCC { cond => i::NE, ... } ) => p90;
	x86branch_prob (i::JCC { cond => i::NO, ... } ) => p100;
	x86branch_prob (i::JCC { cond => i::PP, ... } ) => p0_001; #  fp unordered test 
	x86branch_prob (i::JCC { cond => i::NP, ... } ) => p100;

	x86branch_prob (i::JCC _) => p50; #  Default 
	x86branch_prob (i::JMP _) => p100; 

	x86branch_prob _ => probability::never;
    end

    also
    fun branch_prob (i::NOTE { note, instruction, ... } )
	    => 
	    case (lowcode_notes::branch_prob.peek note)

		  THE b =>   b;
		  NULL  =>   branch_prob instruction;
	    esac;

	branch_prob (i::BASE_INSTRUCTION i)
	    =>
	    x86branch_prob i;

	branch_prob _
	    =>
	    probability::never;
     end;

};



## COPYRIGHT (c) 2002 Bell Labs, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
