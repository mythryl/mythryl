


# binaryC convertor
#
# this generic provides functions to convert binary C data 
# (as stored in e.g. a vector_of_one_byte_unts) to/from structured C data
# (as used in the Lib7-C interface).



###                 "Perfecting oneself is as much unlearning as it is learning."
###
###                                             -- E.J. Dijkstra



generic package  BinaryC (package c:  Ccalls) : BINARY_C {

	package w8v = vector_of_one_byte_unts
	package W8 = Word8

	package c = C
	use C

	toChar = char::from_int o W8::toInt
	fromChar = W8::from_int o char::to_int

	fun vecToCvec v = 
	    let len = w8v::length v
	    in
		(Cvector (vector::tabulate (len,
					 fn i => Cchar (toChar (w8v::sub (v, i))))),
		 CvectorT (len, CcharT))
	    end

	fun cVecToVec (cv, n) = 
	    w8v::tabulate (n, fn i => let my Cchar c = vector::get (cv, i)
				    in fromChar c
				    end)

	fun okTypeForFile CcharT = TRUE
	  | okTypeForFile CintT = TRUE
	  | okTypeForFile CdoubleT = TRUE
	  | okTypeForFile CfloatT = TRUE
	  | okTypeForFile (CstructT l) = 
	    fold_right (fn (x, y) => y and (okTypeForFile x)) TRUE l
	  | okTypeForFile (CarrayT(_, t)) = okTypeForFile t
	  | okTypeForFile (CvectorT(_, t)) = okTypeForFile t
	  | okTypeForFile _ = FALSE

	exception NonFlatFileType
	fun fromBits type v = 
	    let okTypeForFile type or raise exception NonFlatFileType
		my (v, vtype) = vecToCvec v
		#  Warning: the next lines perform a cast 
		my (p, plist) = convert_mythryl_value_to_c (CptrT vtype) (Cptr v)
		my (Cptr result) = convert_c_value_to_mythryl (CptrT type) p
	    in	apply free plist;
		result
	    end

	fun toBits type datum = 
	    let okTypeForFile type or raise exception NonFlatFileType
		szb = sizeof type
		#  Warning: the next lines perform a cast 
		my (p, plist) = convert_mythryl_value_to_c (CptrT type) (Cptr datum)
		my Cptr (Cvector result) = 
		    convert_c_value_to_mythryl (CptrT (CvectorT (szb, CcharT))) p
	    in	apply free plist;
		cVecToVec (result, szb)
	    end

}  #  generic package BinaryCFile 



## COPYRIGHT (c) 1996  Bell Laboratories, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
