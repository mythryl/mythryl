## example-start calendar calendar.c 
## Copyright (C) 2000 Tony Gale
##
## This program is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 2 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program; if not, write to the Free Software
## Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.


local

   include c
   include gtk
   include gtk_enums
   include gtk_macros

   DEF_PAD = 10:  int1::Int
   DEF_PAD' = 0w10:  unt1::word
   DEF_PAD_SMALL = 5:  int1::Int
   TM_YEAR_BASE = 0w1900:  unt1::word

   my & = ptr::enref'

   enum CalendarData = CAL of 
   {  flag_checkboxes:  List( Ref( GtkCheckButton( base ) ) ),
      settings:         List(  REF(  Bool ) ),
      font:             Ref( String ),
      font_dialog:      REF(  GtkFontSelectionDialog(  base ) ),
      window:           REF(  GtkCalendar(  base ) ),
      prev2_sig:        REF(  GtkLabel(  base ) ),
      prev_sig:         REF(  GtkLabel(  base ) ),
      last_sig:         REF(  GtkLabel(  base ) ),
      messages:         REF(  List(  String ) )
   }
   
   enum options = 
     calendar_show_header |
     calendar_show_days |
     calendar_month_change | 
     calendar_show_week |
     calendar_monday_first
   

   # GtkCalendar

  
   # This function converts the current year/month/day into string format 

   fun calendar_date_to_string (CAL { window, ... } )
       =
       let my (year, month, day) = gtk_calendar_get_date::f *window
       in  int::to_string (unt1::toInt  year) + "/"  + 
	   int::to_string (1+unt1::toInt month) + "/"  + 
	   int::to_string (unt1::toInt day)
       end

   fun calendar_set_signal_strings 
         (CAL { prev_sig, last_sig, prev2_sig, messages, ... } ) sig_str
      = 
      (gtk_label_set_text::f (*prev2_sig, list::nth(*messages, 1));
       gtk_label_set_text::f (*prev_sig, list::nth(*messages, 0));
       gtk_label_set_text::f (*last_sig, sig_str);
       messages := sig_str . list::take_n(*messages, 2)
      ) 

   fun calendar_action label cal _
       =
       calendar_set_signal_strings cal (label + calendar_date_to_string cal)
 
   
   fun calendar_month_changed x = calendar_action "month_changed: " x
   
   fun calendar_day_selected x = calendar_action "day_selected: " x
   
   fun calendar_day_selected_double_click (cal as CAL { window, ... } ) _ = 
   let my () = calendar_set_signal_strings cal
          ("day_selected_double_click: " +  calendar_date_to_string cal);
       my (year, month, day) = gtk_calendar_get_date::f (*window)
   in  if get::sint'(
             Arr::sub' (t::sizeof (t::element GtkCalendar::type_marked_date),
                       t::dim GtkCalendar::type_marked_date)
              (GtkCalendar::get'marked_date(*window),
                  (unt1::toInt day - 1))) = 0 then
         gtk_calendar_mark_day::f(*window, day)
       else 
         gtk_calendar_unmark_day::f(*window, day);
       ()
   end
   
   fun calendar_prev_month x = calendar_action "prev_month: " x
   
   fun calendar_next_month x = calendar_action "next_month: " x
   
   fun calendar_prev_year x = calendar_action "prev_year: " x
   
   fun calendar_next_year x = calendar_action "next_year: " x
   
   fun calendar_set_flags (CAL { window, settings, ... } ) = 
   let fun loop ([], i, options: int1::Int) = options
         | loop (s . ss, i, options) =
              loop (ss, i+i, if *s then options + i else options)
       options = loop (settings, 1, 0)
   in  if (not (Ptr::isNull'(*window))) then
          gtk_calendar_display_options::f (*window, options)
       else ()
   end
   
   fun calendar_toggle_flag
         (cal as CAL { flag_checkboxes, settings, ... } ) toggle = 
       (paired_lists::apply (fn (t, f) => if ====(toggle,*t) then f := not(*f) else ())
             (flag_checkboxes, settings);
        calendar_set_flags cal
       )
   
   fun calendar_font_selection_ok 
        (CAL { font, font_dialog, window, ... } ) button =
     (font := gtk_font_selection_dialog_get_font_name::f(*font_dialog);
     if (not (Ptr::isNull'(*window))) then
     let newFont = 
           gtk_font_selection_dialog_get_font::f(*font_dialog)
     in  if (not (Ptr::isNull' newFont)) then
         let style = gtk_style_copy::f (gtk_widget_get_style::f(*window))
         in  gdk_font_unref::f (GtkStyle::get'font style);
             GtkStyle::set'font (style, newFont);
   	     gdk_font_ref::f newFont;
   	     gtk_widget_set_style::f (*window, style)
   	 end
         else ()
     end
     else ()
     )
   
   fun calendar_select_font (calendar as CAL { font_dialog, ... } ) button =
   ( if Ptr::isNull'(*font_dialog) then
     let window = gtk_font_selection_dialog_new::f "Font Selection Dialog"
     in  #  g_return_if_fail::f (GTK_IS_FONT_SELECTION_DIALOG (window)); 

         font_dialog := window;
       
         gtk_window_set_position::f (window, GTK_WIN_POS_MOUSE);
       
         gtk_signal_connect::f (window, "destroy",
                               fn _ => gtk_widget_destroy::f(*font_dialog));

         gtk_signal_connect::f (GtkFontSelectionDialog::get'ok_button window,
   			      "clicked", 
                                calendar_font_selection_ok calendar);

         gtk_signal_connect::f (GtkFontSelectionDialog::get'cancel_button window,
   			      "clicked",
   			       fn _ => gtk_widget_destroy::f (*font_dialog));
         ()
     end
     
     let window = *font_dialog
     in  if not (gtk_widget_visible (window)) then
            gtk_widget_show::f window
         else
           gtk_widget_destroy::f window
     end
   )

   fun create_calendar () =
   let
       flag_checkboxes = [REF NULL, REF NULL, REF NULL, REF NULL, REF NULL]
       settings    = [REF FALSE, REF FALSE, REF FALSE, REF FALSE, REF FALSE]
       font        = REF ""
       font_dialog = REF NULL
       cal_window  = REF NULL
       prev2_sig   = REF NULL
       prev_sig    = REF NULL
       last_sig    = REF NULL
       messages    = REF ["", "", ""]
       flags = 
           [ "Show Heading", 
             "Show Day Names",
             "No Month Change",
             "Show Week Numbers",
             "Week Start Monday"
           ]
       calendar_data = 
           CAL { flag_checkboxes = flag_checkboxes,
                 settings        = settings,
                 font            = font,
                 font_dialog     = font_dialog,
                 window          = cal_window,
                 prev2_sig       = prev2_sig,
                 prev_sig        = prev_sig,
                 last_sig        = last_sig,
                 messages        = messages
               }
 
       window = gtk_window_new::f GTK_WINDOW_TOPLEVEL
       gtk_window_set_title::f (window, "GtkCalendar Example")
       gtk_container_set_border_width::f (window, 0w5)
       gtk_signal_connect::f (window, "destroy", 
                                         fn _ => gtk_main_quit::f())
     
       gtk_window_set_policy::f (window, 0, 0, 1)
     
       vbox   = gtk_vbox_new::f (FALSE, DEF_PAD)
       gtk_container_add::f (window, vbox)
     
       /*
        * The top part of the window, Calendar, flags and fontsel.
        */
     
       hbox  = gtk_hbox_new::f (FALSE, DEF_PAD)
       gtk_box_pack_start::f (vbox, hbox, TRUE, TRUE, DEF_PAD')
       hbbox = gtk_hbutton_box_new::f ()
       gtk_box_pack_start::f (hbox, hbbox, FALSE, FALSE, DEF_PAD')
       gtk_button_box_set_layout::f (hbbox, GTK_BUTTONBOX_SPREAD)
       gtk_button_box_set_spacing::f (hbbox, 5)
     
       #  Calendar widget 
       frame = gtk_frame_new::f "Calendar"
       gtk_box_pack_start::f (hbbox, frame, FALSE, TRUE, DEF_PAD')
       calendar = gtk_calendar_new::f ()
  
       cal_window := calendar;
  
       calendar_set_flags (calendar_data)
       gtk_calendar_mark_day::f (calendar, 0w19)
       gtk_container_add::f (frame, calendar)
  
       /* Add callbacks:
        *   Higher order functions make the initialization more succinct
        */
       
           apply (fn (name, f) =>
             gtk_signal_connect::f (calendar, name, f calendar_data))
              [("month_changed", calendar_month_changed),
               ("day_selected",  calendar_day_selected),
               ("day_selected_double_click", calendar_day_selected_double_click),
               ("prev_month", calendar_prev_month), 
               ("next_month", calendar_next_month),
               ("prev_year", calendar_prev_year),
               ("next_year", calendar_next_year)
              ]
     
       separator = gtk_vseparator_new::f ()
       gtk_box_pack_start::f (hbox, separator, FALSE, TRUE, 0w0)
     
       vbox2     = gtk_vbox_new::f (FALSE, DEF_PAD)
       gtk_box_pack_start::f (hbox, vbox2, FALSE, FALSE, DEF_PAD')
       
       /* Build the Right frame with the flags in */ 
     
       frame     = gtk_frame_new::f "Flags"
       gtk_box_pack_start::f (vbox2, frame, TRUE, TRUE, DEF_PAD')
       vbox3     = gtk_vbox_new::f (TRUE, DEF_PAD_SMALL)
       gtk_container_add::f (frame, vbox3)
     
       fun loop (label, checkbox) = 
       let toggle = gtk_check_button_new_with_label::f label
       in  gtk_signal_connect::f (toggle, "toggled",
     			         calendar_toggle_flag calendar_data);
           gtk_box_pack_start::f (vbox3, toggle, TRUE, TRUE, 0w0);
           checkbox := toggle
       end
       paired_lists::apply loop (flags, flag_checkboxes)
  
       /* Build the right font-button */ 
       button = gtk_button_new_with_label::f "Font..."
       gtk_signal_connect::f (button, "clicked",
     		       calendar_select_font calendar_data)
       gtk_box_pack_start::f (vbox2, button, FALSE, FALSE, 0w0)
     
       /*
        *  Build the Signal-event part.
        */
     
       frame = gtk_frame_new::f "Signal events"
       gtk_box_pack_start::f (vbox, frame, TRUE, TRUE, DEF_PAD')
     
       vbox2 = gtk_vbox_new::f (TRUE, DEF_PAD_SMALL)
       gtk_container_add::f (frame, vbox2)
       
       hbox  = gtk_hbox_new::f (FALSE, 3)
       gtk_box_pack_start::f (vbox2, hbox, FALSE, TRUE, 0w0)
       label = gtk_label_new::f "Signal:"
       gtk_box_pack_start::f (hbox, label, FALSE, TRUE, 0w0)
       last_sig := gtk_label_new::f ""
       gtk_box_pack_start::f (hbox, *last_sig, FALSE, TRUE, 0w0)
     
       hbox  = gtk_hbox_new::f (FALSE, 3)
       gtk_box_pack_start::f (vbox2, hbox, FALSE, TRUE, 0w0)
       label = gtk_label_new::f "Previous signal:"
       gtk_box_pack_start::f (hbox, label, FALSE, TRUE, 0w0)
       prev_sig := gtk_label_new::f ""
       gtk_box_pack_start::f (hbox, *prev_sig, FALSE, TRUE, 0w0)
     
       hbox  = gtk_hbox_new::f (FALSE, 3)
       gtk_box_pack_start::f (vbox2, hbox, FALSE, TRUE, 0w0)
       label = gtk_label_new::f "Second previous signal:"
       gtk_box_pack_start::f (hbox, label, FALSE, TRUE, 0w0)
       prev2_sig := gtk_label_new::f ""
       gtk_box_pack_start::f (hbox, *prev2_sig, FALSE, TRUE, 0w0)
     
       bbox  = gtk_hbutton_box_new::f ()
       gtk_box_pack_start::f (vbox, bbox, FALSE, FALSE, 0w0)
       gtk_button_box_set_layout::f (bbox, GTK_BUTTONBOX_END)
     
       button = gtk_button_new_with_label::f "Close"
       gtk_signal_connect::f (button, "clicked",
                         (fn _ => gtk_main_quit::f ()))
       gtk_container_add::f (bbox, button)
       gtk_widget_set_flags (button, GTK_CAN_DEFAULT)
       gtk_widget_grab_default::f button
     
   in  gtk_widget_show_all::f window
   end
   
in 

   fun main (prog, argv) =
       (gtk_set_locale::f ();
        gtk_init::f (prog . argv);
        create_calendar();
        gtk_main::f();
        0
       )

end #  local 


## Copyright (C) 1998 Cesar Miquel, Shawn T. Amundson, Mattias Grönlund
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
