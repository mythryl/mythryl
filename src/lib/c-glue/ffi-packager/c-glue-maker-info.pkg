local use ml_grinder
in
package lib7_nlffi_gen_info:  Lib7_Nlffi_Gen_Info {
 
    package raw = Ast
    package u   = AstUtil
    package re  = RE

    enum interface = LIGHT | HEAVY

    #  Naming 
    fun filenameToCname filename
	=
	{   base =   winix__premicrothread::path::base filename;
	    re::replace "^.*-+" "" base;
	}

    fun tick LIGHT x = x^"'"
      | tick HEAVY x = x


    fun structnameToCname s
        = 
	if      re::matches "^F_" s            then re::replace "^F_" "" s
	else if re::matches "^[SU]_.*'[0-9]" s then re::replace "^[SU]_+" "" s
	else if re::matches "^I?[SU].*'" s     then re::replace "^.*'" "" s 
	else if re::matches "^I?[SU]T?_" s     then re::replace "^I?[SU]T?_" "" s
	else raise exception FAIL ("structnameToCname: "$s)

    fun structnameToMLname s
        = 
	if re::matches "^[SU]_[0-9]" s
        then s #  Don't strip it 
	else
	      {   s =   structnameToCname s;
		  s =   re::replace "^'" "" s;
		  s =   re::replace "^_+" "" s;		#  strip leading _ 
	          s;
	      }

    fun structnameToMLFunname s
        = 
	{   s = re::replace "^F_" "" s; 

	    if    re::matches "^_" s   then  "f"$s
                                       else  s;
	}

    isAnonymousStruct =   re::matches "^[SU]T?_'?[0-9]"

    exception NoRTTI of raw::ty

    typepath  = ["C", "T"]
    sizePath = ["C", "S"]
    ptrSize  = raw::ID_IN_EXPRESSION (raw::IDENT (sizePath, "ptr"))
    fptrSize = raw::ID_IN_EXPRESSION (raw::IDENT (sizePath, "fptr"))

    pointer = raw::ID_IN_EXPRESSION (raw::IDENT (typepath, "pointer"))
    ro      = raw::ID_IN_EXPRESSION (raw::IDENT (typepath, "ro"))
    arr     = raw::ID_IN_EXPRESSION (raw::IDENT (typepath, "arr"))
    dim'    = raw::ID_IN_EXPRESSION (raw::IDENT (typepath, "dim'"))

    fun pointerOf t  = raw::APPLY_EXPRESSION (pointer, t)
    fun roOf t       = raw::APPLY_EXPRESSION (ro, t)
    fun arrOf (t, dim) = raw::APPLY_EXPRESSION (arr, raw::TUPLE_IN_EXPRESSION [t, dim])

    fun App (path, f, x) = raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT (path, f)), x)

    #  Given an type expression, return runtime type info 
    fun typOf (t as raw::IDTY (raw::IDENT([], ty))) =
	(case ty of
	   ("sint" | "uint" | "schar" | "uchar" |
	    "sshort" | "ushort" | "slong" | "ulong" |
	    "float" | "double" | "voidptr") => raw::ID_IN_EXPRESSION (raw::IDENT (typepath, ty))
	| _ => raise exception NoRTTI t
	)
      | typOf (raw::IDTY (raw::IDENT (SU, "tag"))) = raw::ID_IN_EXPRESSION (raw::IDENT (SU, "type"))
      | typOf (t as raw::APPTY (raw::IDENT([], f), args)) =
	(case (f, args) of 
	  (("ptr" | "ptr'"),[ty]) => pointerOf (typOf ty)
	| (("obj" | "obj'"),[ty, mode]) => modeOf (mode, typOf ty)
	| ("su",[ty]) => typOf ty
	| (("arr" | "arr'"),[ty, dim]) => arrOf (typOf ty, dimOf dim)
	| _ => raise exception NoRTTI t
	)
      | typOf ty = raise exception NoRTTI ty

    and dimOf (raw::IDTY (raw::IDENT([], "dim"))) = dim'
      | dimOf (t as raw::APPTY (raw::IDENT([], dg),[ty])) =
	if re::matches "^dg[0-9]$" dg then
	    raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT (typepath, dg$"'")), dimOf ty)
	else 
	   raise exception FAIL("dimOf: "$Ty::show t)
      | dimOf ty = raise exception FAIL("dimOf: "$Ty::show ty)

    and modeOf (raw::IDTY (raw::IDENT([], "ro")), type) = roOf type
      | modeOf(_, type) = type

    and sizeTy t = raw::APPTY (raw::IDENT(["C", "S"], "size"),[t])

    #  Given an type expression, return runtime size info 
    and sizeOf (t as raw::IDTY (raw::IDENT([], ty))) =
	(case ty of
	   ("sint" | "uint" | "schar" | "uchar" |
	    "sshort" | "ushort" | "slong" | "ulong" |
	    "float" | "double" | "voidptr") => raw::ID_IN_EXPRESSION (raw::IDENT (sizePath, ty))
	|  _ => raise exception NoRTTI t
	)
      | sizeOf (raw::IDTY (raw::IDENT (SU, "tag"))) = raw::ID_IN_EXPRESSION (raw::IDENT (SU, "size"))
      | sizeOf (t as raw::APPTY (raw::IDENT([], f), args)) =
	(case (f, args) of 
	   (("ptr" | "ptr'"), _) => 
	     raw::TYPED_EXPRESSION (ptrSize, sizeTy (raw::APPTY (raw::IDENT([], "ptr"), args)))
	 | (("fptr" | "fptr'"), _) => 
	     raw::TYPED_EXPRESSION (fptrSize, sizeTy (raw::APPTY (raw::IDENT([], "fptr"), args)))
	 | ("su", [ty]) => sizeOf ty
	 | (("obj" | "obj'"), [ty, _]) => sizeOf ty
	 | _ => raise exception NoRTTI t
	)
      | sizeOf t = raise exception NoRTTI t

    #  Given an type expression, return its get function 
    fun getSetOf path interface (t as raw::IDTY (raw::IDENT([], ty))) =
	(case ty of
	   ("sint" | "uint" | "schar" | "uchar" |
	    "sshort" | "ushort" | "slong" | "ulong" |
	    "float" | "double" | "voidptr") => 
	       raw::ID_IN_EXPRESSION (raw::IDENT (path, tick interface ty))
	| ("ubf" | "sbf") => raw::ID_IN_EXPRESSION (raw::IDENT (path, ty))
	|  _ => raise exception NoRTTI t
	)
      | getSetOf path interface (t as raw::APPTY (raw::IDENT([], f), args)) =
	(case (f, args) of 
	   (("ptr" | "ptr'"), _) => raw::ID_IN_EXPRESSION (raw::IDENT (path, tick interface "ptr"))
	 | (("fptr" | "fptr'"), _) => raw::ID_IN_EXPRESSION (raw::IDENT (path, tick interface "fptr"))
	 | ("obj", [ty, _]) => getSetOf path HEAVY ty
	 | ("obj'", [ty, _]) => getSetOf path LIGHT ty
	 | _ => raise exception NoRTTI t
	)
      | getSetOf _ _ t = raise exception NoRTTI t

    getOf = getSetOf ["C", "Get"]
    setOf = getSetOf ["C", "Set"]


    #  Create a tag definition 

    fun tagDecl { ty, name, su }
	=
	{   fun make t
                =
                "t_"$str t$" ";

	    tag  =   string::cat (map make (string::explode name));

	    A.@@@["local use Tag",
		"in",
		"    type "$ty$" = " $tag$ " " $su,
		"end"
	       ];
	}

    signedTy   =   raw::IDTY (raw::IDENT(["mlrep", "Signed"],   "int" ))
    unsignedTy =   raw::IDTY (raw::IDENT(["mlrep", "Unsigned"], "word"))
    realTy     =   raw::IDTY (raw::IDENT(["mlrep", "Float"],     "real"))

    fun repTy interface (ty as raw::IDTY (raw::IDENT([], t)))
        =
	 (case t of
	   ("sint" | "schar" | "sshort" | "slong") => signedTy
	 | ("uint" | "uchar" | "ushort" | "ulong") => unsignedTy
	 | ("float" | "double") => realTy
	 | _ => ty
	 )
      | repTy interface (ty as raw::APPTY (raw::IDENT (path, f), args)) = 
	(case f of 
	   ("ptr" | "ptr'") => raw::APPTY (raw::IDENT (path, tick interface "ptr"), args) 
	 | ("fptr" | "fptr'")  => 
	    raw::APPTY (raw::IDENT (path, tick interface "fptr"), args) 
	 | ("su" | "arr") => ty
	 | ("obj" | "obj'") => ty
	 | "enum" => signedTy #  XXX 
	 | _ => raise exception NoRTTI ty
	)
      | repTy _ t = raise exception NoRTTI t



    #  Remove all generated enum stuff by c-glue-maker 

    fun noBuiltinEnums decl
	=
	{   isEnum =   re::matches "^E_";

	    fun exp e
                = 
		case e of
		  raw::ID_IN_EXPRESSION (raw::IDENT([E], "c2sint")) =>
		    if isEnum E then raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "ml_sint")) else e
		| raw::ID_IN_EXPRESSION (raw::IDENT([E], "sint2c")) => 
		    if isEnum E then raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "c_sint")) else e
		| raw::ID_IN_EXPRESSION (raw::IDENT([E], "type")) => 
		    if isEnum E then raw::ID_IN_EXPRESSION (raw::IDENT(["C", "T"], "sint")) else e
		| raw::ID_IN_EXPRESSION (raw::IDENT(["T"], "enum")) => raw::ID_IN_EXPRESSION (raw::IDENT(["T"], "sint"))
		| raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT([E], "c")), a) =>
		    if isEnum E then raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "c_sint")), a) 
		    else e
		| raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT([E], "ml")), a) =>
		    if isEnum E then raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "ml_sint")), a)
		    else e
		| raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "i2c_enum")), a) =>
		    raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "c_sint")), a)
		| raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "c2i_enum")), a) =>
		    raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["Cvt"], "ml_sint")), a)
		| e => e;

	    fun ty t = 
		case t of
		  raw::IDTY (raw::IDENT([E], "mlrep")) =>
		    if isEnum E then raw::IDTY (raw::IDENT(["mlrep", "Signed"], "int"))
		    else t
		| raw::IDTY (raw::IDENT([E], "type")) =>
		    if isEnum E then raw::IDTY (raw::IDENT([], "sint"))
		    else t
		| raw::APPTY (raw::IDENT([], "enum"),[_]) => raw::IDTY (raw::IDENT([], "sint"))
		| t => t;

	    decl::map[Map::E exp, Map::T ty] decl;
	}


    #  Code generation 
    package code {

       fun apply i (p, x, y) = raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT (p, tick i x)), y)
       fun applyX i (p, x, y) = apply i (p, x, u::ID y)
       fun ignore e = raw::VAL_DECL [raw::Named_Value (raw::TUPLEPAT [], e)]
       fun new i (x, ty) = u::MY (x, apply i (["C"], "new", sizeOf ty))
       fun alloc i (x, n, ty) = 
	      u::MY (x, raw::APPLY_EXPRESSION (apply i (["C"], "alloc", sizeOf ty),
				 raw::LITERAL_IN_EXPRESSION (raw::UNT_LIT (unt::from_int n))))
       fun discard i x = ignore (applyX i (["C"], "discard", x))
       fun free i x = ignore (applyX i (["C"], "free", x))
       fun dupML i (x, y) = u::MY (x, applyX i (["ZString"], "dupML", y))
       fun toML i (x, y) = u::MY (x, applyX i (["ZString"], "toML", y))

       fun & i (x, y) = 
	   u::MY (x, raw::APPLY_EXPRESSION (raw::ID_IN_EXPRESSION (raw::IDENT(["C", "Ptr"],
			       case i of LIGHT => "enref'" | HEAVY => "enref")),
			    u::ID y))
    }



    package t {

	fun any _ = TRUE

	fun id f (raw::IDTY (raw::IDENT([], x))) = f x
	  | id _ _ = FALSE

	prim = id any

	fun primObj f g (raw::APPTY (raw::IDENT([], obj),[t])) =
	      re::matches "_obj$" obj and f t and g obj
	  | primObj _ _ _ = FALSE

	fun AnyTy tc (f, rw) (raw::APPTY (raw::IDENT([], tc'), [t, r])) = 
				   tc=tc' and rw r and f t
	  | AnyTy _ _ _ = FALSE

	fun AnyPtr ptr f (raw::APPTY (raw::IDENT([], p), [t])) = ptr=p and f t
	  | AnyPtr _ _ _ = FALSE

	fun rw (raw::IDTY (raw::IDENT([], "rw"))) = TRUE | rw _ = FALSE
	fun ro (raw::IDTY (raw::IDENT([], "ro"))) = TRUE | ro _ = FALSE

	ptr  = AnyPtr "ptr"
	ptr' = AnyPtr "ptr'"
	obj  = AnyTy "obj"
	obj' = AnyTy "obj'"
	arr  = AnyTy "arr"
	arr' = AnyTy "arr'"

	fun getRW (raw::IDTY (raw::IDENT([], x))) = x
	  | getRW t = raise exception FAIL ("getRW: "$Ty::show t)

	fun getObj (raw::APPTY (raw::IDENT([], "obj"),[t, rw])) = THE (t, getRW rw)
	  | getObj (raw::APPTY (raw::IDENT([], "su_obj"),[t, rw])) =
	     THE (raw::APPTY (raw::IDENT([], "su"),[t]), getRW rw)
	  | getObj (raw::APPTY (raw::IDENT([], obj),[t as raw::IDTY _])) =
	    (case re::grep "^(.*)_obj$" obj of
	      THE[x] => THE (raw::IDTY (raw::IDENT([], x)), getRW t)
	    | _ => NULL
	    )
	  | getObj _ = NULL

	fun getTy ptr (raw::APPTY (raw::IDENT([], p),[t])) =
	    if p == ptr then getObj t else NULL
	  | getTy _ _ = NULL

	getPtr  = getTy "ptr"
	getPtr' = getTy "ptr'"
    }

    package raw {

	fun bytesOf s
	    =
	    unt::toIntX (c::s::toWord s);

	fun bytes (raw::IDTY (raw::IDENT([], "schar"))) = bytesOf c::s::schar
	  | bytes (raw::IDTY (raw::IDENT([], "uchar"))) = bytesOf c::s::uchar
	  | bytes (raw::IDTY (raw::IDENT([], "sshort"))) = bytesOf c::s::sshort
	  | bytes (raw::IDTY (raw::IDENT([], "ushort"))) = bytesOf c::s::ushort
	  | bytes (raw::IDTY (raw::IDENT([], "sint"))) = bytesOf c::s::sint
	  | bytes (raw::IDTY (raw::IDENT([], "uint"))) = bytesOf c::s::uint
	  | bytes (raw::IDTY (raw::IDENT([], "slong"))) = bytesOf c::s::slong
	  | bytes (raw::IDTY (raw::IDENT([], "ulong"))) = bytesOf c::s::ulong
	  | bytes (raw::IDTY (raw::IDENT([], "float"))) = bytesOf c::s::float
	  | bytes (raw::IDTY (raw::IDENT([], "double"))) = bytesOf c::s::double
	  | bytes (raw::APPTY (raw::IDENT([], ("ptr'" | "ptr")), _)) = bytesOf c::s::ptr
	  | bytes t = raise exception NoRTTI t;

	fun access (raw::IDTY (raw::IDENT([], x))) = x
	  | access (raw::APPTY (raw::IDENT([], ("ptr'" | "ptr")), _)) = "ptr"
	  | access t = raise exception NoRTTI t;

	fun roundToWords  n =   (n + 3) div 4;
	fun roundToDwords n =   (n + 7) div 8;
	fun words t         =   (bytes t + 3) div 4;

	fun alloc (x, ty, n)
	    = 
	    {   size =   bytes ty;

		my (a, n)
                    = 
		    if   size == 8
                    then
		         ("c_alloc64", roundToDwords (size * n))
		    else
		         ("c_alloc", roundToWords (size * n));

	        u::MY (x, App(["c_ext"], a, u::integer_constant_in_expression n));
	    }

	fun set (x, ty, i, y)
	    = 
	    {   n =   bytes ty ;

	        u::MY("_", App(["c_ext", "Set"], access ty,
			   raw::TUPLE_IN_EXPRESSION [u::ID x, u::WORDexp (unt::from_int (i*n)), u::ID y]));
	    }

	fun get (x, y, ty, i)
	    = 
	    {   n =   bytes ty;

	        u::MY (x, App(["c_ext", "Get"], access ty,
			     raw::TUPLE_IN_EXPRESSION [u::ID y, u::WORDexp (unt::from_int (i*n))]));
	    }

	fun c_string (x, y)
	    =
	    u::MY (x, App(["c_ext"], "c_string", u::ID y))

	 /*
	    raw::SEQ_DECL[
	       u::MY (x, App(["c_ext"], "c_string", u::ID y)),
	       comment["Empty string is broken in smlnj!"],
	       u::MY (x, raw::IFexp (App(["c_ext"], "boxed", u::ID x),
			  u::ID x,
			  raw::LETexp([u::MY (x, App(["c_ext"], "c_alloc", u::integer_constant_in_expression 1))],
			     [App(["c_ext", "Set"], "uchar",
				  raw::TUPLE_IN_EXPRESSION [u::ID x, u::WORDexp 0w0, u::WORDexp 0w0]),
			      u::ID x
			     ])))
	   ]
	  */
    }
};
end;



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################
