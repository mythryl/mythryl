## memalloc-a4-win32.pkg
## Author: Matthias Blume (blume@tti-c.org)

#   Memory allocation (via GlobalAlloc) for Win32.
#   Size of address: 4 bytes.
#
package c_mem_alloc:  Cmem_Alloc {

    exception OUT_OF_MEMORY;

    Addr  = unt32::word;
    Addr' = address;

    package dl = dynamic_linkage;

    fun main's s
        =
        dl::lib_symbol (dl::main_lib, s);

    malloc_h =   main's "GlobalAlloc";
    free_h   =   main's "GlobalFree";

    fun sys_malloc (n:  unt32::word)
        =
	{   w_p = raw_mem_inline_t::rawccall :
		      unt32::word * (unt32::word * unt32::word) *
		      List( Void * word * word -> String )
		      -> unt32::word;
	    a = w_p (dl::address malloc_h, (0w0, n), []);

	    if a == 0w0 then raise OUT_OF_MEMORY; else a; fi;
	};

    fun sys_free (a:  unt32::word)
	=
	{   p_u = raw_mem_inline_t::rawccall :
		      unt32::word * unt32::word *
		      List (Void * String -> Void)
		      -> Void;
	    p_u (dl::address free_h, a, []);
	};

    fun alloc bytes =  sys_malloc (unt::toLargeWord bytes);
    fun free a      =  sys_free a;
};


## Copyright (c) 2004 by The Fellowship of SML/NJ
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
