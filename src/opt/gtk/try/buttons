#!/usr/bin/mythryl

# This is a Mythryl version of the GTK+ tutorial
# fancy-button example:
#
#     http://library.gnome.org/devel/gtk-tutorial/stable/c491.html
#

include package gtk_client_for_server_in_c_subprocess;

fun xpm_label_box (session, xpm_filename, label_text)
    =
    {   box = make_horizontal_box { session, homogeneous => FALSE, spacing => 0 };
        set_border_width (session, box, 2);

        image = make_image_from_file (session, xpm_filename);
        label = make_label (session, label_text);

	pack_box { session, box, kid => image, pack => FROM_START, expand => FALSE, fill => FALSE, padding => 3 };
	pack_box { session, box, kid => label, pack => FROM_START, expand => FALSE, fill => FALSE, padding => 3 };

        show_widget (session, image);
        show_widget (session, label);

        box;
    };

fun callback string
    =
    printf "Hello again -- %s was pressed\n"  string;

session = make_session [];

window = make_window session;

set_window_title (session, window, "Pixmap'd Buttons!");

set_delete_event_callback session window {. exit_main session; exit 0; };
set_destroy_callback      session window {. exit_main session; exit 0; };

set_border_width (session, window, 10);

button = make_button session;
set_clicked_callback session button  {. callback "cool button";  };

box = xpm_label_box (session, "info.xpm", "cool button");

show_widget (session, box   );

add_kid { session, kid => box, mom => button };

show_widget (session, button);

add_kid { session, kid => button, mom => window };

show_widget (session, window);

main session;

exit 0;

