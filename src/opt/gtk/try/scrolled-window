#!/usr/bin/mythryl

# This is a Mythryl version of the GTK+ tutorial
# scrolled-window example program:
#
#     http://library.gnome.org/devel/gtk-tutorial/stable/x1370.html
#

include package   gtk_client_for_server_in_c_subprocess;


session = make_session [];

(make_dialog  session)
    ->
    { dialog => window, vbox, action_area };

set_delete_event_callback session window {. exit_main session; exit 0; };
set_destroy_callback      session window {. exit_main session; exit 0; };

set_window_title (session, window, "GtkScrolledWindow example");

set_border_width (session, window, 0);

set_minimum_widget_size (session, window, (300, 300));



scrolled_window = make_scrolled_window (session, NULL, NULL);

set_border_width (session, scrolled_window, 10);

set_scrollbar_policy
    { session,
      window               =>  scrolled_window,
      horizontal_scrollbar =>  POLICY_AUTOMATIC,
      vertical_scrollbar   =>  POLICY_ALWAYS
    };

pack_box { session, box => vbox, kid => scrolled_window,  pack => FROM_START, expand => TRUE,  fill => TRUE, padding => 0 };
show_widget (session, scrolled_window); 

table = make_table { session, rows => 10, cols => 10, homogeneous => FALSE };

set_table_row_spacings (session, table, 10);
set_table_col_spacings (session, table, 10);

add_scrolled_window_kid { session, window => scrolled_window, kid => table };
show_widget (session, table);

# Nested 'for' loops don't work at the moment:  :-/
#
foreach     (0..9) {. i = #i;
    foreach (0..9) {. j = #j;
	button = make_toggle_button_with_label (session, sprintf "button (%d, %d)\n" i j);
        add_table_kid { session, table, kid => button, left => i, right => i+1, top => j, bottom => j+1 };
	show_widget (session, button);
    };
};

button = make_button_with_label (session, "close");
set_clicked_callback session button {. destroy_widget( session, window ); };

# XXX BUGGO FIXME need to do the grab-default stuff here also.

show_widget (session, button);
show_widget (session, window);

main session;

exit 0;
