#!/usr/bin/mythryl

# This is a Mythryl version of the first
# "hello, world" GTK+ 2.0 tutorial example at
#     
#     http://library.gnome.org/devel/gtk-tutorial/stable/x345.html

# Compare with:
#     try/gtk/easy-hello-world-2

include gtk_client_for_server_in_c_subprocess;

fun callback string
    =
    printf "Hello again -- %s was pressed\n"  string;

session = make_session [];

window =  make_window  session;

set_window_title (session, window, "Hello Buttons!");

set_delete_event_callback session window .{ print "DELETE_EVENT\n"; exit_main session; exit 0; };

set_border_width (session, window, 10);

box = make_horizontal_box { session, homogeneous => FALSE, spacing => 0 };

add_kid { session, mom => window, kid => box };

button = make_button_with_label (session, "Button 1");

set_clicked_callback session button  .{ callback "button 1";  };

pack_box
    { session,
      box,
      kid     =>  button,
      expand  =>  TRUE,
      fill    =>  TRUE,
      padding =>  0,
      pack    =>  FROM_END
    };

show_widget (session, button);

button = make_button_with_label (session, "Button 2");

set_clicked_callback session button  .{ callback "button 2";  };

pack_box
    { session,
      box,
      kid     =>  button,
      expand  =>  TRUE,
      fill    =>  TRUE,
      padding =>  0,
      pack    =>  FROM_END
    };

show_widget (session, button);
show_widget (session, box   );
show_widget (session, window);

main session;

exit 0;
